 LIST OFF
PARM LINK(DECSZ023 R)
*=====================================================================
*=== DECSZ023 READS 2 PRINT FILES - INITIAL INCOME ASSIGNMENT COPIES
*=== AND MODIFY INCOME ASSIGNMENT COPIES, BOTH FOR CS OFFICES.
*===
*=== COUNTS NUMBER OF MAILING PACKETS FOR EACH CITY, A.K.A. CS OFFICE.
*=== COUNTS ARE EMAILED, PRINTED, AND DISPLAYED.
*=====================================================================

*=== FILE BELOW CONTAINS NAMES OF CITIES WITH CHILD SUPPORT OFFICES.
*=== NAMES ARE USED TO BUILD A TEMP TBL, FOR SEARCHING, & PRINTING.
 FILE IPCITIES FB (80 0)
  IP-CITY-REC         1   80  A
    IP-CITY-NAME      1   20  A
    IP-CITY-NAME-6POS 1    6  A


*=== FILE BELOW CONTAINS CHILD SUPPORT OFFICES'S COPIES
*=== OF INCOME ASSIGNMENTS.
*=== RECORD TYPES 1 - 6 MAKE ONE MAILING PACKET.
 FILE INFILE FB (982 0)
  IP-CS-REC             1  982  A
    IP-CS-REC-NUM       1    1  A
    IP-CS-MAIL-CITY1  278   20  A
    IP-CS-MAIL-CITY2  311   20  A

*=== REPORT GETS WRITTEN TO A FILE, AND CATALOGED.  GOES INTO EMAIL.
 FILE REPORT1 PRINTER FB (80 0)

 FILE REPORT2 PRINTER FB (80 0)


*=== IP MAIL CITY HAS 2 POSSIBLE LOCATIONS, BASE ON NUMBER OF LINES
*=== IN ADDRESS.  SO FIND THE CITY, AND SAVE IT HERE.
  SEARCH-FOR-THIS-CITY  W  20  A
    SEARCH-FOR-THIS-CITY-6POS    SEARCH-FOR-THIS-CITY     6 A
    SEARCH-FOR-THIS-CITY-6TH-POS SEARCH-FOR-THIS-CITY +5  1 A

  TBL-CITY-NAME     W  20  A               OCCURS 101 INDEX PNTR
    TBL-CITY-NAME-6POS      TBL-CITY-NAME 6   A

  TBL-CITY-PACK-CNT W   5  N  MASK 'ZZZZ9' OCCURS 101 INDEX PNTR

  WS-TOTAL-PACKETS  W   7  N  MASK 'ZZZZZZ9'

  PNTR-MINUS1       W   2  P


  REPORT-CITY         W    20  A
  REPORT-CNT          W     5  N MASK 'ZZZZ9'
  REPORT-TOTAL-PACKET S     7  N MASK 'Z,ZZZ,ZZ9'
  REPORT-MESSAGE      W    48  A
  REPORT-MSG1         REPORT-MESSAGE       20  A
  REPORT-MSG2         REPORT-MESSAGE +20   28  A

JOB INPUT(INFILE) NAME(DECSZ023) START(GET-CITIES) FINISH(END-OF-JOB)
    IF IP-CS-REC-NUM > '1'
        GO TO JOB
    END-IF

    WS-TOTAL-PACKETS = 1 + WS-TOTAL-PACKETS.

    IF IP-CS-MAIL-CITY2 > ' '
        SEARCH-FOR-THIS-CITY = IP-CS-MAIL-CITY2
    ELSE
        SEARCH-FOR-THIS-CITY = IP-CS-MAIL-CITY1
    END-IF

*=== SOMETIMES A COMMA IS WITH MAILING CITY. EX:  "PARIS, TN 38242"
*=== THE "CARD" LIST OF CITIES WILL NOT HAVE THE COMMA.
*=== SO SPACING OUT THE COMMA WILL LET THE COMPARE WORK AS DESIRED.
    IF SEARCH-FOR-THIS-CITY-6TH-POS = ','
        MOVE ' ' TO SEARCH-FOR-THIS-CITY-6TH-POS
    END-IF

    PNTR = 0.
    PERFORM FIND-CITY-MATCH.
    GO TO JOB.

*=== SEARCH FOR CS CITY NAME TO MATCH ONE IN THE TEMP TABLE.
*=== SEARCH IS MADE ON 1ST 6 POS OF CITY NAME, BECAUSE CS MAIL CITY
*=== IS IN A FORMATTED MAIL ADDRESS.  IF WE USED MORE POSITIONS,
*=== CITY "NAMES" COULD INCLUDE STATE AND ZIP CODE: "PARIS TN 38242"
FIND-CITY-MATCH. PROC
CONTINUE-CITY-MATCH.
    PNTR = 1 + PNTR.
    IF SEARCH-FOR-THIS-CITY-6POS = TBL-CITY-NAME-6POS (PNTR)
        TBL-CITY-PACK-CNT (PNTR) = 1 + TBL-CITY-PACK-CNT (PNTR)
        GO TO END-CITY-MATCH-PROC
    END-IF

    IF TBL-CITY-NAME-6POS (PNTR) = ' '  OR  +
       PNTR = 101
        PRINT UNEXPECTED-CITY-RPT
        TBL-CITY-NAME (101) = SEARCH-FOR-THIS-CITY
        TBL-CITY-PACK-CNT (101) = 1 + TBL-CITY-PACK-CNT (101)
        GO TO END-CITY-MATCH-PROC
    ELSE
        GO TO CONTINUE-CITY-MATCH
    END-IF.
END-CITY-MATCH-PROC.
END-PROC.

*=== BUILD A TABLE OF CS OFFICE CITIES, FROM A "CARD" FILE.
GET-CITIES. PROC
CONTINUE-CITIES.
    GET IPCITIES
    IF EOF IPCITIES
        GO TO END-CITIES-PROC
    END-IF

    PNTR = 1 + PNTR.
    IF PNTR > 100
        DISPLAY ' '
        DISPLAY '====================================== ***'
        DISPLAY 'TOO MANY CITIES FOUND IN "CARD" FILE.  ***'
        DISPLAY 'DECSZ023 ALLOWS MAX OF 100, FOUND 101. ***'
        DISPLAY 'ABORTING PROGRAM DECSZ023.             ***'
        DISPLAY '====================================== ***'
        DISPLAY ' '
        RETURN-CODE = 299
        STOP EXECUTE
    END-IF

    TBL-CITY-NAME (PNTR) = IP-CITY-NAME.
    GO TO CONTINUE-CITIES.
END-CITIES-PROC.
    IF PNTR = 0
        DISPLAY ' '
        DISPLAY '===================================== ***'
        DISPLAY 'NO CITIES FOUND IN "CARD" FILE.       ***'
        DISPLAY 'ABORTING PROGRAM DECSZ023.            ***'
        DISPLAY '===================================== ***'
        DISPLAY ' '
        RETURN-CODE = 199
        STOP EXECUTE
    END-IF
END-PROC.

*=== WRITE REPORT AFTER COUNTING PACKETS.
*=== "DISPLAYS" PUT CNTS INTO INFOPAC JOB LIST.
END-OF-JOB. PROC
    IF TBL-CITY-PACK-CNT (101) > 0
        REPORT-CITY   = '>> UNEXPECTED <<'
        REPORT-CNT    = TBL-CITY-PACK-CNT (101)
        REPORT-MSG1   = TBL-CITY-NAME (101)
        REPORT-MSG2   = '  SHOW TO PROGRAMMER.'
        PRINT PACKS-BY-CITY-RPT
    END-IF.
    PNTR = 1
CONTINUE-END-OF-JOB.
    IF TBL-CITY-NAME (PNTR) > ' '
        REPORT-MESSAGE = ' '
        IF PNTR > 1
            PNTR-MINUS1 = PNTR - 1
            IF TBL-CITY-NAME-6POS (PNTR)  +
                = TBL-CITY-NAME-6POS (PNTR-MINUS1)
                REPORT-MSG1   = '1ST 6 POS DUPLICATES'
                REPORT-MSG2   = ' ABOVE CITY. SHOW PROGRAMMER.'
            END-IF
        END-IF
        REPORT-CITY   = TBL-CITY-NAME (PNTR)
        REPORT-CNT    = TBL-CITY-PACK-CNT (PNTR)
        PRINT PACKS-BY-CITY-RPT
        PNTR = 1 + PNTR
    ELSE
        GO TO END-OF-EOJ-PROC
    END-IF.

    IF PNTR > 100
        GO TO END-OF-EOJ-PROC
    END-IF.

    GO TO CONTINUE-END-OF-JOB.
END-OF-EOJ-PROC.
    REPORT-TOTAL-PACKET = WS-TOTAL-PACKETS
END-PROC.

*====================================================================
*===  REPORT  PROCESSING  ===
   REPORT PACKS-BY-CITY-RPT PRINTER REPORT1 LINESIZE 79 SPACE 2 +
          TITLESKIP 0  NOADJUST NODATE NOPAGE NOHEADING
      TITLE 1 'REPORT: DECSZ023        DHS - CHILD SUPPORT  ' +
               '        DATE' SYSDATE-LONG
      TITLE 3 '                     CS OFFICE PACKETS BY CITY  ' +
               '              PAGE  1'
      TITLE 4 '               FOR MAILING INCOME ASSIGNMENT COPIES'
      TITLE 5 ' '
      TITLE 6 '       CITY          PACKETS      MESSAGE'
      LINE  1  REPORT-CITY -2 '=' -2 REPORT-CNT  +
               -1 REPORT-MESSAGE

*=== THIS PUTS TOTAL PACKETS AT END OF REPORT.
TERMINATION. PROC
    DISPLAY 'TOT PACKET COUNT=' REPORT-TOTAL-PACKET
    DISPLAY '                          ***  END OF REPORT ***'
END-PROC.

*====================================================================
*===  REPORT  PROCESSING  ===
   REPORT UNEXPECTED-CITY-RPT PRINTER REPORT2 LINESIZE 79 SPACE 2 +
          TITLESKIP 0  NOADJUST NODATE NOPAGE NOHEADING
      TITLE 1 'REPORT: DECSZ023        DHS - CHILD SUPPORT  ' +
               '        DATE' SYSDATE-LONG
      TITLE 2 '                  FOUND UNEXPECTED CS-OFFICE CITY  ' +
               '           PAGE  1'
      TITLE 3 '                 FOR MAILING INCOME ASSIGNMENT COPY'
      TITLE 4 ' '
      LINE  1 'UNEXPECTED  CITY NAME FOUND:' SEARCH-FOR-THIS-CITY
      LINE  2 'PLEASE DETERMINE IF IT SHOULD BE ADDED TO OUR'
      LINE  3 'CITY LIST IN DE.DE24APDS.PROD.CARD(CSCITIES)'
      LINE  4 ' '
TERMINATION. PROC
    DISPLAY '                          ***  END OF REPORT ***'
END-PROC.
