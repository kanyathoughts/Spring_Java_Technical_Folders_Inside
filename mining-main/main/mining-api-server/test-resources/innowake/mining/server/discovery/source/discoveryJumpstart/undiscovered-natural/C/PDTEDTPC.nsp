/* PDTEDTPC * Hochladen von Dateien über den DTM-PC               /* RSE        
/*      
DEFINE DATA         
 GLOBAL USING UAMGDA01
  LOCAL  USING UAMLGPAR         
  LOCAL  USING UAMAADTT         
  LOCAL 
    1 DTS-WKF       
      2 LINE-NR           (N8)  
      2 LINE-LEN          (N4)  
      2 TEXT              (A250)
      2 REDEFINE TEXT           
        3 HEADER          (A12) 
        3 FUNKTION        (A12) 
      2 REDEFINE TEXT           
        3 TEXT-BIN-1      (B125)
        3 TEXT-BIN-2      (B125)
  LOCAL 
    1 HILFSVARIABLEN
      2 #RC               (I1)     /* Returncode        
      2 #ZEILE-1          (A60)    /* Erste Zeile der Datei         
      2 #FUNKTION         (A12)    /* Auftragsfunkton   
      2 #PREFIX           (A3)     /* Prefis des Auftrags           
      2 #SATZENDE         (A8)     /* Satzendekennung   
      2 #CODE             (A200)   /* Codesystem der Datei          
      2 #QUELLDATEI       (A60)    /* Quelldatei        
      2 #SICHERUNG        (A60)    /* Sicherungsverzeichnis         
      2 #HEADER           (A2)     /* Datei hat Header  
END-DEFINE          
/*-------------------------------------------------- Initialisierung ---        
CALLNAT 'UAMZNPON'  
/*      
SET CONTROL 'N'     
SET CONTROL 'WL47C13B5/16FS'    
INPUT USING MAP 'UAMAMDM2'      
SET CONTROL 'WB'    
/*      
ASSIGN #G-PARM(*) = G-UAMPARM(*)
/*      
REPEAT  
  RESET G-UAMRC(11) 
/*------------------------------------------------ Auftrag ermitteln ---        
  CLOSE WORK FILE 5 
  DOWNLOAD PC FILE 5 COMMAND 'ROKDVUP'      
  CLOSE WORK FILE 5 
/*      
  CALLNAT 'UAMZNSYC'
/*      
  READ WORK FILE 5 ONCE DTS-WKF 
  CLOSE WORK FILE 5 
/*      
  IF DTS-WKF.LINE-NR EQ 99999999 AND DTS-WKF.LINE-LEN EQ 9999 THEN  
    COMPRESS ' Der Datentransfer Nummer' UAMAADTT.DTE-COMAREA 'wurde'           
      'erfolgreich durchgeführt!' INTO G-UAMREINPUT     
    FETCH G-UAMRETPROG          
  END-IF
/*-------------------------------------- Auftragsparameter ermitteln ---        
  CLOSE WORK FILE 7 
  READ WORK FILE 7 ONCE #ZEILE-1    /* Erste Zeile der Datei        
  READ WORK FILE 7 ONCE #FUNKTION   /* Auftragsfunkton  
  READ WORK FILE 7 ONCE #PREFIX     /* Prefis des Auftrags          
  READ WORK FILE 7 ONCE #SATZENDE   /* Satzendekennung  
  READ WORK FILE 7 ONCE #CODE       /* Codesystem der Datei         
  READ WORK FILE 7 ONCE #SICHERUNG  /* Sicherungsverzeichnis        
  READ WORK FILE 7 ONCE #HEADER     /* Datei hat Header 
  READ WORK FILE 7 ONCE #QUELLDATEI /* Quelldatei       
  CLOSE WORK FILE 7 
/*----------------------------------------------- Auftrag abarbeiten ---        
  ASSIGN UAMAADTT.AUFTRAGSFUNKTION = #FUNKTION          
  ASSIGN UAMAADTT.PREFIX           = #PREFIX
  ASSIGN UAMAADTT.TRANSFER-PLANEN  = TRUE   
  ASSIGN UAMAADTT.USEREXIT         = 'NDTEDTPC'         
  ASSIGN UAMAADTT.DATEI            = #QUELLDATEI        
  ASSIGN UAMAADTT.SICHERUNG        = #SICHERUNG         
  ASSIGN UAMAADTT.KONVERTIERUNG    = #CODE  
/*      
  IF #HEADER EQ '-1' THEN       
    ASSIGN UAMAADTT.ZEILE-1 = #ZEILE-1      
  END-IF
/*      
  IF G-UAMREFAPPL EQ 65535 THEN 
    IF #A-TYP EQ 'N' THEN       
      ASSIGN UAMAADTT.TYP = 'S' 
    ELSE
      ASSIGN UAMAADTT.TYP = #A-TYP          
    END-IF          
    ASSIGN UAMAADTT.MD  = I00003-G-UAMMD    
    ASSIGN UAMAADTT.UMD = I00004-G-UAMUMD   
  ELSE  
    ASSIGN UAMAADTT.TYP = 'U'   
    ASSIGN UAMAADTT.MD  = G-UAMMANDANT      
    ASSIGN UAMAADTT.UMD = G-UAMUMANDANT     
  END-IF
/*      
  CALLNAT 'UAMANDTT'
    G-UAMMF (*) G-UAMINFO G-UAMTITEL G-UAMF1 G-UAMF2    
    G-UAMPFT(*) G-UAMPF1(*) G-UAMPFB(*) UAMGDA1(*) G-UAMPARM(*)     
    UAMAADTT        
/*------------------------------------------- Ablaufmanager aufrufen ---        
/* Falls die Auftragserfassung nicht erfolgreich durchgeführt werden
/* konnte, wird via des API's UAMMNTVA ein Abbruch des Teilvorgangs 
/* ausgelöst.       
/*      
  IF UAMAADTT.RC NE 0 THEN      
    CALLNAT 'UAMMNTVA'          
      G-UAMMF (*) G-UAMINFO G-UAMTITEL G-UAMF1 G-UAMF2  
      G-UAMPFT(*) G-UAMPF1(*) G-UAMPFB(*) #RC           
  END-IF
END-REPEAT          
/*      
FETCH G-UAMRETPROG  
/*----------------------------------------------------------------------        
END
