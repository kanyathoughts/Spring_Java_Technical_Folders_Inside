Form CEPL503AA

    /*  Processes the Authorizations procedure/service data entry screen.
        In update mode either stores or updates records, Inquiry mode
	displays records.   It is processed by CEPS503AA.COB         */

    Form Data 
	FIRST_TIME_FLAG CHARACTER(1) VALUE "N"
        HEADER_1 Character(30)
        HEADER_2 Character(30)
        PAR_HEADER_1 Character(30)
        PAR_HEADER_2 Character(30)

        AUTH_BEN_ADM Character(9)
	LINK_ID CHARACTER(4)
	LINK_NAME CHARACTER(33)
	LINK_NAME2 CHARACTER(33)
					   /* Program Control Variables   */

        SCREEN_MODE Character(8)           /* UPDATE/INQUIRY disp field   */
        PROG_MODE Character(1)		   /* U => Update; I => Inquiry   */
	UPD_MODE Character(1)		   /* S => User is scrolling thru
						   procedures
					      I => User is inserting new
						   procedures
					      U => User updating existing
						   procedure              */
	RETURN_COND Character(3)	   /* Return condition from here  */
	PROCESS_COND Character(3)	   /* What user wants to do next  */
        CHGS_MADE Longword Integer	   /* Anything changed?           */
	CURR_PROC Unsigned Word		   /* current procedure in window */
	FIRST_PROC Unsigned Word	   /* top procedure in window     */
	PROC_LIMIT Unsigned Word Value 10  /* Limit for number of procs   */

	AUTH_ID Character(7)	        /* Auth Master fields of interest */
	AUTH_TYPE Character(1)
	AUTH_SSN Character(9)
	AUTH_MBR_TYPE Character(1)
	AUTH_PMG_ID Character(4)
	AUTH_SERV_PROV_NAME Character(44)
	AUTH_PRI_DIAG Character(5)
	AUTH_SEC_DIAG Character(5)
	AUTH_TER_DIAG Character(5)

        AUTH_TYPE_DESC Character(36)       /* Auth Master data description */
	AUTH_MBR_NAME Character(31)        /*  fields we'll need           */
	AUTH_MBR_TYPE_DESC Character(3)
        AUTH_PMG_NAME Character(33)
        AUTH_PMG_NAME2 Character(27)

        Group PROCEDURE			    /* Array of procedures/services */
            Occurs 20
	    Base 1
	    Current CURR_PROC
            PROC_ID Character(5)               /* RVS Code             */
	    PROC_MOD Character(2)	       /* Modifier             */
            PROC_POS Character(1)              /* Place Of Service     */
            PROC_SVC_CODE Word Integer	       /* Service Code         */
            PROC_OCC_APPR Word Integer         /* Occurrences approved */
            PROC_OCC_USED Word Integer         /* Occurrences used     */
            PROC_AMT_APPR Longword Integer     /* Amount approved      */
            PROC_AMT_USED Longword Integer     /* Amount used          */
            PROC_EST_BEN_AMT Longword Integer  /* Calc Est Benefit amt */
	    PROC_AUTH_DATE Longword Integer    /* Authorized date      */
	    PROC_FLAG Character(1)	       /* Process flag         */
	    PROC_SVC_DESC Character(8)	       /* Service description  */
	    PROC_AUTH_VMSDATE Date	       /* Authorized VMS Date  */
	    PROC_ACTIVE_FLAG Character(1)      /* Active Procedure flag*/
	    PROC_ITEM_NO Word Integer          /* Need to save this... */
					       /*  part of primary key */
	    PROC_LINKED_FLAG Character(1)
        End Group

        PROC_OCC_APPR_TOTAL Word Integer      /* buckets for screen totals */   
        PROC_OCC_USED_TOTAL Word Integer
        PROC_AMT_APPR_TOTAL Longword Integer
        PROC_AMT_USED_TOTAL Longword Integer
        PROC_EST_BEN_AMT_TOTAL Longword Integer

        ABORT_FLD Character(5)             /* Things for Enter, Abort &  */
        CONFIRM_FLD Character(1)	   /*  Confirm panel processing  */
        CONFIRM_FLAG Word Integer
        CONFIRM_PROMPT_FLD Character(38)
        ENTER_FLD Character(2)
        ENTER_RESPONSE Character(6)
        OPTION_FLD Character(1)
        OPTION_DESC Character(20)
        OPTION_TERSE Character(20)
        OPTION_TABLE Character(12)
        OPTION_DIRECTION Character(1)
	DEL_FLD Character(1)	
	FUNC_CODE Character(1)
	CURR_VMSDATE Date Current
	TODAY_VMSDATE Date
	TODAY_ICIDATE Longword Integer
	TODAY_ICITIME Longword Integer
	ICIDATE1 Longword Integer
	ICITIME1 Longword Integer
	ICIDATE2 Longword Integer
	ICITIME2 Longword Integer
	NONE Word Integer Value 0
	ONE Word Integer Value 1
	ONE_HUNDRED Word Integer Value 100
	MINUS_ONE Word Integer Value -1
	TWO Word Integer Value 2
	THREE Word Integer Value 3
	FOUR Word Integer Value 4
	MINUS_FOUR Word Integer Value -4
	SAVE_PTR Word Integer Value 0
	LONG_TEMP Longword Integer 
	WORD_TEMP Word Integer
	SERV_CAT Character(1)
	SAVE_CAT Character(1)
	SAVE_PROCID Character(5)
	PROC_ID_DESC Character(15)
	SVC_DESCRIPTION Character(15)
	SVC_CODE_DESC Character(4)
	SVC_CODE_HUNDREDS Character(2)
	SVC_CODE_TENS Character(2)

	HEADER_NODE_ID CHARACTER(3)
	HEADER_ENV_ID CHARACTER(3)
        TODAY Date Current

	Copy "CLMINC:CEP_FORMS_GEN_DATA.INC" /* Contains data defs for Table */
	End Copy 			     /*    access functions, etc.    */

	Copy "INT_RECORDS.HN_Namedef" From Dictionary
	End Copy

    End Data

    Copy "CLMINC:CEP_FORMS_503_MSGS.INC"   
    End Copy


    Copy "CLMINC:CEP_FORMS_GEN_RECS.INC"   /* Table access, etc. records  */ 
    End Copy

    Form Record LINK_REC
	LINK_ID CHARACTER(4)
	LINK_NAME CHARACTER(33)
	LINK_NAME2 CHARACTER(33)
    End Record

    Form Record AUTPROC_REC
	AUTH_ID Character(7)	
	AUTH_TYPE Character(1)
	AUTH_SSN Character(9)
	AUTH_MBR_TYPE Character(1)
	AUTH_PMG_ID Character(4)
	AUTH_SERV_PROV_NAME Character(44)
	AUTH_PRI_DIAG Character(5)
	AUTH_SEC_DIAG Character(5)
	AUTH_TER_DIAG Character(5)
        Group PROCEDURE 
            Occurs 20
            PROC_ID Character(5)
	    PROC_MOD Character(2)
            PROC_POS Character(1)
            PROC_SVC_CODE Word Integer
            PROC_OCC_APPR Word Integer
            PROC_OCC_USED Word Integer
            PROC_AMT_APPR Longword Integer
            PROC_AMT_USED Longword Integer
            PROC_EST_BEN_AMT Longword Integer
	    PROC_AUTH_DATE Longword Integer
	    PROC_FLAG Character(1)
	    PROC_ITEM_NO Word Integer  
	    PROC_SVC_DESC Character(8)
	    PROC_AUTH_VMSDATE Date
	    PROC_LINKED_FLAG Character(1)
        End Group
    End Record

    Form Record PROC_ARRAY_REC
        Group PROCEDURE 
            Occurs 20
            PROC_ID Character(5)
	    PROC_MOD Character(2)
            PROC_POS Character(1)
            PROC_SVC_CODE Word Integer
            PROC_OCC_APPR Word Integer
            PROC_OCC_USED Word Integer
            PROC_AMT_APPR Longword Integer
            PROC_AMT_USED Longword Integer
            PROC_EST_BEN_AMT Longword Integer
	    PROC_AUTH_DATE Longword Integer
	    PROC_FLAG Character(1)
	    PROC_ITEM_NO Word Integer        
	    PROC_ACTIVE_FLAG Character(1)
	    PROC_SVC_DESC Character(8)
	    PROC_AUTH_VMSDATE Date
	    PROC_LINKED_FLAG Character(1)
        End Group
    End Record

    Form Record PROC_TOTALS_REC
        PROC_OCC_APPR_TOTAL Word Integer 
        PROC_OCC_USED_TOTAL Word Integer
        PROC_AMT_APPR_TOTAL Longword Integer
        PROC_AMT_USED_TOTAL Longword Integer
        PROC_EST_BEN_AMT_TOTAL Longword Integer
    End Record

    Form Record GENF126AA_REC
	AUTH_SSN Character(9)
	AUTH_MBR_TYPE Character(1)
	AUTH_MBR_NAME Character(31)
        AUTH_MBR_DOB_MMDD Character(4)
        AUTH_MBR_DOB_CCYY Character(4)
	AUTH_MBR_FLAGS Longword Integer
    End Record

    Form Record GEN_NAME_REC
    	Copy "INT_RECORDS.Hn_Namedef" From Dictionary
    End Copy
    End Record

    Form Record PROC_STAMP
	TODAY_ICIDATE Longword Integer
	TODAY_ICITIME Longword Integer
	AUTH_LAST_UPD_USERNAME Character(3)
    End Record

    Form Record SVC_CODE_REC
	SVC_CODE_HUNDREDS Character(2)
	SVC_CODE_TENS Character(2)
    End Record

    Form Record RET_PROC_COND
	RETURN_COND Character(3)
	PROCESS_COND Character(3)
    End Record

    Record List RET_CODE_REC_LIST
	AUTPROC_REC
	RET_PROC_COND
    End List


    Layout VT_LAYOUT
        Device
            Terminal
                Type %VT100
        End Device
        Size 24 Lines by 80 Columns

        Viewport HEADER_VIEWPORT
            Lines 1 Through 1
            Columns 1 Through 80
        End Viewport

        Viewport MAIN_VP
            Lines 2 Through 22
            Columns 1 Through 80
        End Viewport

        Viewport PROMPT_VIEWPORT
            Lines 23 Through 24
            Columns 1 Through 80
        End Viewport

					    /* Standard func. key definitions */
	Copy "CLMINC:CEP_FORMS_PFKEY_DEFS.INC" 
	End Copy                              

	Function TAB_KEY
	    Is %HORIZONTAL_TAB
	End Function	      

	COPY "CLMINC:CEP_FORMS_GEN_RESP.INC" /* General responses to validate */
	End Copy			     /*  tables, show messages, etc.  */


	Internal Response SHOW_CODES	    /* For Debugging  */
	    Message 
		"PROCESS: " PROCESS_COND " RETURN: " RETURN_COND
	End Response


        Internal Response INIT_HDR
	    CALL "HN_FRM_HDR_INFO" USING
		BY REFERENCE HEADER_NODE_ID
		BY REFERENCE HEADER_ENV_ID
            Call "ICTRNLNM_XLATE_01" Using
                    By Descriptor "ICSCR_HDR1"
                    By Descriptor PAR_HEADER_1
		    Giving RET_VALUE
	    	IF PAR_HEADER_1 = "" THEN
		   LET HEADER_1 = " Claims Administration System "
		ELSE
		   LET HEADER_1 = PAR_HEADER_1
		END IF
		
            Call "ICTRNLNM_XLATE_01" Using
                    By Descriptor "ICSCR_HDR2"
                    By Descriptor PAR_HEADER_2
		    Giving RET_VALUE
	    	IF PAR_HEADER_2 = "X Development" THEN
		   LET HEADER_2 = "    X Development    "
		ELSE
		   LET HEADER_2 = PAR_HEADER_2
		END IF
        End Response

        Internal Response DISPLAY_AUTH  /* Translate all codes and get  */
					/* table descriptions for auth  */
            Let TABLE_ID = "AUTH_TYPE"  
            Let TABLE_TYPE = "P"	
            Let TABLE_CODE = AUTH_TYPE
            Include VALIDATE_TABLE
            Let AUTH_TYPE_DESC = TABLE_DESC

	    Call "ENRF037AA_CVT_MBR_CODE" Using	  /* Convert MBR_TYPE byte */   
		By Descriptor AUTH_MBR_TYPE	  /* integer into desc.    */
		By Descriptor AUTH_MBR_TYPE_DESC 
		By Descriptor "1"
		Giving RET_VALUE
	    If RET_VALUE = FALSE Then
		Let AUTH_MBR_TYPE_DESC = "***"      /* Invalid code  */
	    End If

	    Call "GENF126AA_Get_Mbr_Name" Using
		By Reference GENF126AA_REC
	    Giving RET_VALUE

	    Let LINK_ID = AUTH_PMG_ID
	    Call "NETF001AA_GET_MASTER" Using
		  By Reference LINK_REC
	    Giving RET_VALUE 
	    If RET_VALUE = FALSE Then
		Let AUTH_PMG_NAME = "** Invalid PMG"
		Let AUTH_PMG_NAME2 = ""
	    Else
		Let AUTH_PMG_NAME = LINK_NAME
		Let AUTH_PMG_NAME2 = LINK_NAME2
	    End If	

	    Call "CEPF507AA_Calc_Totals" Using
		By Reference PROC_TOTALS_REC
		By Reference PROC_ARRAY_REC
		Giving RET_VALUE

	    RESET OPTION_FLD OPTION_DESC ENTER_FLD 
        End Response

        Internal Response ABORT_REQUEST    /* Handle PF1, PF2 & PF4 keys  */
	    If CHGS_MADE = 1 And PROG_MODE = "U" Then
		Include CONFIRM_ABORT
	    Else
		Let PROCESS_COND = "    "
		If RETURN_COND = "PF4" Then
		    Let PROCESS_COND = "MAN"
		End If
	    	RESET OPTION_FLD OPTION_DESC ENTER_FLD 
		Remove All
		Return Immediate
	    End If
        End Response

        Internal Response ENTER_PROMPT		 /* Put user on ENTER FIELD: */
            Deactivate
                Field OPTION_FLD on ENTER_PANEL
            Activate
                Field ENTER_FLD on ENTER_PANEL

            Position Immediate To Field ENTER_FLD On ENTER_PANEL
        End Response

        Internal Response OPTION_PROMPT		 /* Put user on OPTION:      */
            Deactivate
                Field ENTER_FLD on ENTER_PANEL
            Activate
                Field OPTION_FLD on ENTER_PANEL
            Position Immediate To Field OPTION_FLD On ENTER_PANEL
        End Response

        Internal Response CONFIRM_DELETE 
	    Deactivate
		Panel ENTER_PANEL
		Panel ABORT_PANEL  
		Panel AUTPROC_PANEL
	    Activate
		Panel DEL_PANEL
	    Position Immediate to Field DEL_FLD On DEL_PANEL   
        End Response

        Internal Response CONFIRM_ABORT   /* Show Abort Panel if user has */
					  /* input or changed anything    */
	    Deactivate	
		Panel ENTER_PANEL
		Panel CONFIRM_PANEL
		Panel AUTPROC_PANEL
	    Display 
		ABORT_PANEL
            Activate
                Panel ABORT_PANEL
            Position Immediate To Field ABORT_FLD On ABORT_PANEL
        End Response

	Internal Response RESET_CURR_PROC
		Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG   = " "
		Let PROCEDURE(CURR_PROC).PROC_FLAG          = " "
		Let PROCEDURE(CURR_PROC).PROC_ID            = " " 
		Let PROCEDURE(CURR_PROC).PROC_MOD           = " "
		Let PROCEDURE(CURR_PROC).PROC_POS           = " "
		Let PROCEDURE(CURR_PROC).PROC_LINKED_FLAG   = " "
		Let PROCEDURE(CURR_PROC).PROC_SVC_DESC      = "       "
		Let PROCEDURE(CURR_PROC).PROC_SVC_CODE      = -1
		Let PROCEDURE(CURR_PROC).PROC_OCC_APPR      = -1
		Let PROCEDURE(CURR_PROC).PROC_OCC_USED      = -1
		Let PROCEDURE(CURR_PROC).PROC_AMT_APPR      = -1
		Let PROCEDURE(CURR_PROC).PROC_AMT_USED      = -1
		Let PROCEDURE(CURR_PROC).PROC_EST_BEN_AMT   = -1
		Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE  = "1858 11 17"
		Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE     = 0
	End Response

	Internal Response RESET_TOTALS
	    Reset
		PROC_OCC_APPR_TOTAL 
		PROC_OCC_USED_TOTAL 
		PROC_AMT_APPR_TOTAL
		PROC_AMT_USED_TOTAL 
		PROC_EST_BEN_AMT_TOTAL
	End Response

	Internal Response RESET_ACTIVE_FLAGS
	    Reset 
		PROCEDURE(1).PROC_ACTIVE_FLAG
		PROCEDURE(2).PROC_ACTIVE_FLAG
		PROCEDURE(3).PROC_ACTIVE_FLAG
		PROCEDURE(4).PROC_ACTIVE_FLAG
		PROCEDURE(5).PROC_ACTIVE_FLAG
		PROCEDURE(6).PROC_ACTIVE_FLAG
		PROCEDURE(7).PROC_ACTIVE_FLAG
		PROCEDURE(8).PROC_ACTIVE_FLAG
		PROCEDURE(9).PROC_ACTIVE_FLAG
		PROCEDURE(10).PROC_ACTIVE_FLAG
	End Response

	Send Response MESSAGE_REC
	    Message MESSAGE_LINE
	End Response


        Transceive Response RET_CODE_REC_LIST RET_CODE_REC_LIST 
            Display
		HEADER_PANEL
                AUTPROC_PANEL
                ENTER_PANEL
            Activate
                Panel AUTPROC_PANEL
                Panel CONFIRM_PANEL
	    Include RESET_ACTIVE_FLAGS

	    If HEADER_NODE_ID = "" Then
		Include INIT_HDR

		Call "GENS029AA_Get_Stamp" Using
		    By Reference PROC_STAMP

		Call "GENF122AA_CVT_ICI_VMS" Using      
		    By Reference TODAY_ICIDATE  
		    By Reference TODAY_VMSDATE
		Giving RET_VALUE                    
	    End If

	    Let CHGS_MADE = 0
	    Include DISPLAY_AUTH

	    If CURR_PROC = 1 Then
		Let PROCEDURE(1).PROC_ACTIVE_FLAG = "X"   
	    ELSE
		Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"   
	    End If           

	    If PROCEDURE(1).PROC_ID = " " And 
	       PROCEDURE(1).PROC_SVC_CODE = -1 Then
		Let UPD_MODE = "I"
		Include RESET_TOTALS
	    Else
		Let UPD_MODE = "S"
	    End If

	    If RETURN_COND = "ADD" Or RETURN_COND = "UPD" Then
		Let CHGS_MADE = 1
	    Else
		Let CHGS_MADE = 0
	    End If

	    If RETURN_COND = "ADD" Then
		Position To Field PROCEDURE(1).PROC_ID On AUTPROC_PANEL
	    Else
		Include ENTER_PROMPT
	    End If

        End Response

        Control Text Response "UPD"
            Let PROG_MODE = "U"
            Let SCREEN_MODE = "UPDATE"
        End Response

        Control Text Response "INQ"
            Let PROG_MODE = "I"
	    Let UPD_MODE = " "
            Let SCREEN_MODE = "INQUIRY"
        End Response

        Function Response PF1_QUIT
            Let RETURN_COND = "PF1"
            Include ABORT_REQUEST
        End Response

        Function Response PF2_QUIT
            Let RETURN_COND = "PF2"
            Include ABORT_REQUEST
        End Response

        Function Response PF4_QUIT
            Let RETURN_COND = "PF4"
            Include ABORT_REQUEST
        End Response


        Panel HEADER_PANEL
            Viewport HEADER_VIEWPORT
            Field HEADER_NODE_ID
                Line 1
                Column 2
            End Field

            Literal Text
                Line 1
                Column 5
                Value "::"
            End Literal

            Field HEADER_ENV_ID
                Line 1
                Column 7
            End Field

            Field HEADER_1
                Line 1
                Column 26
            End Field

            Field TODAY
                Line 1
                Column 69
            End Field

        End Panel



        Panel ABORT_PANEL

            Viewport PROMPT_VIEWPORT

            /* Confirm abort request */

	    Remove

            Literal Text
                Line 1
                Column 30
                Value "Enter RETURN to continue, 'ABORT' to abort"
            End Literal

            Field ABORT_FLD
                Line 1
                Column 74

		Entry Response
		    MESSAGE ""
		    Reset
			ABORT_FLD
		End Response

                Function Response PF1_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF2_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF4_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response ENTER_KEY 

                If ABORT_FLD = "ABORT" Then
                   If RETURN_COND = "PF1" Or RETURN_COND = "PF2" Or RETURN_COND = "PF3"  Then
		        Let PROCESS_COND = "   "
			Deactivate All
			Remove All
                        Return Immediate
		   End If
		   If RETURN_COND = "PF4" Then
		        Let PROCESS_COND = "MAN"
			Deactivate All
	    		RESET OPTION_FLD OPTION_DESC ENTER_FLD 
			Remove All
			Return Immediate
		   End If
	        Else
                   If ABORT_FLD = "     " Then
			Deactivate 
			    Panel ABORT_PANEL
			Display 
			    AUTPROC_PANEL
			    ENTER_PANEL
			Activate 
			    Panel AUTPROC_PANEL
			    Panel ENTER_PANEL
			If UPD_MODE = "U" Or UPD_MODE = "I" Then
			    Position Immediate To 
				Field PROCEDURE(CURR_PROC).PROC_ID
				On AUTPROC_PANEL                   
			Else If UPD_MODE = "S" Then
				Position Immediate To 
				    Field PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG
				    On AUTPROC_PANEL        
			     Else
				Include ENTER_PROMPT
			     End If
			End If
                   Else
                      Include INVALID_FUNCTION
		   End If
                End If
		End Response

                Active Highlight
                    Bold
                    Reverse
                Display
                    Reverse    
                Uppercase

            End Field

        End Panel


        Panel CONFIRM_PANEL
            Viewport PROMPT_VIEWPORT

	    Remove

            /* Confirm Delete or Changes */

            Literal Text
                Line 1
                Column 62
		Value "Confirm Y/N :"
            End Literal

	    Field CONFIRM_FLD
                Line 1
                Column 77

		ENTRY RESPONSE
		    LET CONFIRM_FLD = "N"
		END RESPONSE

		Function Response BACK_FLD
		    Include INVALID_FUNCTION
		End Response

		Function Response LEFT_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response RIGHT_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response UP_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response DOWN_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response INSERT_KEY
		    Include INVALID_FUNCTION
		End Response

		Function Response REMOVE_KEY
		    Include INVALID_FUNCTION
		End Response

		Function Response PREV_PAGE
		    Include INVALID_FUNCTION
		End Response

		Function Response NEXT_PAGE
		    Include INVALID_FUNCTION
		End Response

                Function Response PF1_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF2_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF4_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response ENTER_KEY 
                    If ((CONFIRM_FLD <> "Y") AND (CONFIRM_FLD <> "N")) Then
			Let MSG_ID = GEN_ENTYORN
			Include SHOW_MESSAGE
			Invalid
		    End If
		    If CONFIRM_FLD = "Y" Then
			IF RETURN_COND = "AAD" THEN
			   Let RETURN_COND  = "AAD"
			   Let PROCESS_COND = "PPC"
			ELSE
			   Let RETURN_COND  = "UDF"
			   Let PROCESS_COND = "   "
			END IF
			Return Immediate   
		    Else
			Deactivate		     
			    Panel CONFIRM_PANEL 
			Activate 
			    Panel ENTER_PANEL
			Position Immediate To Field ENTER_FLD On ENTER_PANEL
		    End If
                End Response
		Input Required
                Uppercase
            End Field

        End Panel


        Panel ENTER_PANEL
            Viewport PROMPT_VIEWPORT

            /* Option & Enter prompts */

            Literal Text
                Line 1
                Column 2
                Value "Option:"
            End Literal

            Literal Text
                Line 1
                Column 70
                Value "ENTER:"
            End Literal

            Field ENTER_FLD
                Line 1
                Column 77

		Entry Response
		    Let ENTER_FLD = "" 
/*		    If UPD_MODE = "S" And
		            (PROCEDURE(CURR_PROC).PROC_ID <> " " Or 
			     PROCEDURE(CURR_PROC).PROC_SVC_CODE <> -1) Then 
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
		    End If    */
		    Let UPD_MODE = " "
		End Response

		Function Response BACK_FLD
		    Include INVALID_FUNCTION
		End Response

                Function Response PF1_QUIT
                    Let RETURN_COND = "PF1"
                    Include ABORT_REQUEST
                End Response

                Function Response PF2_QUIT
                    Let RETURN_COND = "PF2"
                    Include ABORT_REQUEST
                End Response

                Function Response PF4_QUIT
                    Let RETURN_COND = "PF4"
                    Include ABORT_REQUEST
                End Response

                Function Response PF3_KEY
                    Let ENTER_RESPONSE = "PF3"
                    Include INVALID_FUNCTION
                End Response

                Function Response LEFT_ITEM
                    Include OPTION_PROMPT
                End Response

		Function Response RIGHT_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response SELECT_KEY
		    /* Later this will call CEPS504AA  */

		    Include INVALID_FUNCTION
		End Response

		Function Response ENTER_KEY     
		    Message
			" "
		    If ENTER_FLD <> "" And ENTER_FLD <> " " Then 
			If PROG_MODE = "U" Then
			    Let MSG_ID = GEN_USEENTKEY
			    Include SHOW_MESSAGE
			    Invalid
			Else 
			    Let MSG_ID = GEN_USEPFKEY
			    Include SHOW_MESSAGE
			    Invalid
			End If
		    Else
			IF PROG_MODE = "I" THEN
			    Let RETURN_COND = "PF4"
			    Let PROCESS_COND = "MAN"
	    		    RESET OPTION_FLD OPTION_DESC ENTER_FLD 
			    Return Immediate
			ELSE
			    Activate 
				Panel CONFIRM_PANEL
			    Position Immediate to 
				Field CONFIRM_FLD On CONFIRM_PANEL
			End If
		     End If

		End Response

		Function Response INSERT_KEY
		    If PROG_MODE = "U" Then
			Let UPD_MODE = "I"
			Let FUNC_CODE = "I"
			Call "CEPF509AA_SCROLL_ARRAY" Using 
			    By Reference FUNC_CODE
			    By Reference PROC_ARRAY_REC
			Giving RET_VALUE
			If RET_VALUE = FALSE Then
			    Let MSG_ID = CEP_MAXPRCLIM
			    CALL "GENF032AA_DISPLAY_MESSAGE" Using
				By Descriptor SESSION
				By Descriptor "MESSAGE_REC"
				By Value      MSG_ID
				By Reference  SCR_COLS
				By Value  PROC_LIMIT
			    Giving RET_VALUE
			    Let UPD_MODE = "S"
			    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			    Position Immediate To 
				Field ENTER_FLD On ENTER_PANEL
		        Else
			    Position Immediate 
				To Field PROCEDURE(CURR_PROC).PROC_ID 
				On AUTPROC_PANEL
			End If
		    Else 
			Let MSG_ID = GEN_INSINV
			Include SHOW_MESSAGE
		    End If

		End Response

		Function Response REMOVE_KEY
		  If PROG_MODE = "U" Then
		    Include CONFIRM_DELETE
		    Position Immediate To Field ENTER_FLD On ENTER_PANEL
		  Else
		    Let MSG_ID = GEN_REMINV
		    Include SHOW_MESSAGE
		  End If
		    
		End Response

		Function Response DO_KEY
		  If PROG_MODE = "U" Then
		    Let UPD_MODE = "U"
		    Position Immediate
			To Field PROCEDURE(CURR_PROC).PROC_ID On AUTPROC_PANEL
		  Else
		    Let MSG_ID = GEN_MODNOTALL
		    Include SHOW_MESSAGE
		  End If

		End Response

		Function Response UP_ITEM
		    MESSAGE ""
		  If CURR_PROC > 1 Then
		    Let UPD_MODE = "S"
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference MINUS_ONE
		    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    Position To 
			Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			On AUTPROC_PANEL
		  Else
		    Position To Field PROCEDURE(1).PROC_ACTIVE_FLAG
			On AUTPROC_PANEL
		    Let PROCEDURE(1).PROC_ACTIVE_FLAG = "X"
		    Let MSG_ID = CEP_FSTPRCDSP
		    Include SHOW_MESSAGE
		  End If
		End Response   

		Function Response DOWN_ITEM
		    MESSAGE ""
		    Let UPD_MODE = "S"
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference ONE
		    If PROCEDURE(WORD_TEMP).PROC_ID = " " And 
		       PROCEDURE(WORD_TEMP).PROC_SVC_CODE = -1 Then
			Let MSG_ID = CEP_LSTPRCDSP
			Include SHOW_MESSAGE
			Invalid
		    Else
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
			Position Immediate To 
			    Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
		    End If
		End Response   

		Function Response PREV_PAGE
		    MESSAGE ""
		    Let UPD_MODE = "S"
		    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    Let WORD_TEMP = CURR_PROC
		    If WORD_TEMP > FOUR Then
		        Call "GENS110AA_Calc_Word" Using
			    By Reference WORD_TEMP
			    By Reference MINUS_FOUR
			Position To 
			    Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    Else
			DEActivate Field ENTER_FLD on ENTER_PANEL
			Position To Field PROCEDURE(1).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(1).PROC_ACTIVE_FLAG = "X"
			Let MSG_ID = CEP_FSTPRCDSP
			Include SHOW_MESSAGE
		    End If
		End Response   

		Function Response NEXT_PAGE
		    MESSAGE ""
		    Let UPD_MODE = "S"
		    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference FOUR
		    If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	Let WORD_TEMP = CURR_PROC
		    	Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference ONE
		    	If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
			   Position To 
			      Field PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG
			      On AUTPROC_PANEL
			   Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			   Let MSG_ID = CEP_LSTPRCDSP
			   Include SHOW_MESSAGE
			ELSE
		    	   Let WORD_TEMP = CURR_PROC
		    	   Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference TWO
		    	   If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	      Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference MINUS_ONE
			      Position To 
			        Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			        On AUTPROC_PANEL
			      Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			      Let MSG_ID = CEP_LSTPRCDSP
			      Include SHOW_MESSAGE
			   ELSE
		    	      Let WORD_TEMP = CURR_PROC
		    	      Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference THREE
		    	      If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	         Call "GENS110AA_Calc_Word" Using
					By Reference WORD_TEMP
					By Reference MINUS_ONE
			         Position To 
			            Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			            On AUTPROC_PANEL
			         Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			         Let MSG_ID = CEP_LSTPRCDSP
			         Include SHOW_MESSAGE
			      ELSE
		    	         Let WORD_TEMP = CURR_PROC
		    	   	 Call "GENS110AA_Calc_Word" Using
				 	By Reference WORD_TEMP
					By Reference FOUR
		    	   	 If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	      	    Call "GENS110AA_Calc_Word" Using
					By Reference WORD_TEMP
					By Reference MINUS_ONE
			      	    Position To 
			        	Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			        	On AUTPROC_PANEL
			      	    Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			            Let MSG_ID = CEP_LSTPRCDSP
			            Include SHOW_MESSAGE
				 END IF
			      END IF
			  END IF
		      END IF
		    Else
			DEActivate Field ENTER_FLD on ENTER_PANEL
			Position To 
			    Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    End If
		End Response    

            End Field

            Field OPTION_FLD
                Line 1
                Column 10
                Entry Response
                    Let TABLE_TYPE = "P"
                    Let TABLE_ID = "AUTH_MAINT"
                    Include TABLE_INIT
                    Let OPTION_FLD = TABLE_CODE_OUT
                    Let OPTION_DESC = TABLE_DESC
                End Response

		Function Response BACK_FLD
		    Include INVALID_FUNCTION
		End Response

                Function Response ENTER_KEY 
                    Let TABLE_CODE = OPTION_FLD
                    Include VALIDATE_TABLE
                    If VALID_OPTION = 0 Then
			Let MSG_ID = GEN_INVOPT
			Include SHOW_MESSAGE
			Invalid
                    End If
                    If OPTION_FLD = "M" Then
			Let RETURN_COND = "   "
			Let PROCESS_COND = "MAN"
			Deactivate
			    All
	    		RESET OPTION_FLD OPTION_DESC ENTER_FLD 
			Remove All
			Return Immediate
                    Else
			If OPTION_FLD = "P" Then
			    Let MSG_ID = CEP_PRCSCRDSP
			    Include SHOW_MESSAGE
			Else
			    Let MSG_ID = GEN_INVOPT
			    Include SHOW_MESSAGE
			End If
		        Invalid
                    End If
                End Response

                Function Response UP_ITEM
                    Let TABLE_DIR = "P"
                    Include SCROLL_TABLE
                    Let OPTION_FLD = TABLE_CODE_OUT
                    Let OPTION_DESC = TABLE_DESC
                End Response

                Function Response DOWN_ITEM
                    Let TABLE_DIR = "N"
                    Include SCROLL_TABLE
                    Let OPTION_FLD = TABLE_CODE_OUT
                    Let OPTION_DESC = TABLE_DESC
                End Response

                Function Response RIGHT_ITEM
		    Include ENTER_PROMPT
                End Response

		Uppercase

            End Field

            Field OPTION_DESC
                Line 1
                Column 12
                Protected
            End Field

        End Panel


        Panel DEL_PANEL

            Viewport PROMPT_VIEWPORT

	    Remove

            /* See if the user really wants to delete the procedure */

            Literal Text
                Line 1
                Column 37
                Value "Are you sure you want to delete? [N]: "
            End Literal

	    Field DEL_FLD
                Line 1
                Column 77

		Function Response BACK_FLD
		    Include INVALID_FUNCTION
		End Response

		Function Response LEFT_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response RIGHT_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response UP_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response DOWN_ITEM
		    Include INVALID_FUNCTION
		End Response

		Function Response INSERT_KEY
		    Include INVALID_FUNCTION
		End Response

		Function Response REMOVE_KEY
		    Include INVALID_FUNCTION
		End Response

		Function Response PREV_PAGE
		    Include INVALID_FUNCTION
		End Response

		Function Response DO_KEY
		    Include INVALID_FUNCTION
		End Response

		Function Response NEXT_PAGE
		    Include INVALID_FUNCTION
		End Response

                Function Response PF1_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF2_QUIT
                    Include INVALID_FUNCTION
                End Response

                Function Response PF4_QUIT
                    Include INVALID_FUNCTION
                End Response

		Entry Response
		    Let DEL_FLD = "N"
		    MESSAGE ""
		End Response

                Function Response ENTER_KEY 
                    If (DEL_FLD <> "Y" AND DEL_FLD <> "N") Then
			Let MSG_ID = GEN_ENTYORN
			Include SHOW_MESSAGE
			Invalid
		    End If
		    If DEL_FLD = "Y" Then
			Let CHGS_MADE = 1
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			Let PROCEDURE(CURR_PROC).PROC_FLAG = "D"
			Let FUNC_CODE = "D"
			Call "CEPF509AA_Scroll_Array" Using
			    By Reference FUNC_CODE
			    By Reference PROC_ARRAY_REC
			Giving RET_VALUE                    

	    		Call "CEPF507AA_Calc_Totals" Using
			    By Reference PROC_TOTALS_REC
			    By Reference PROC_ARRAY_REC
			Giving RET_VALUE
		    End If                                   
		    Deactivate		     
			Panel DEL_PANEL 
		    Activate 
			Panel AUTPROC_PANEL
		    Display
			ENTER_PANEL
		    Activate Field ENTER_FLD on ENTER_PANEL 
		    Position to Field ENTER_FLD on ENTER_PANEL 
                End Response

		Input Required
                Uppercase
            End Field

        End Panel


        Panel AUTPROC_PANEL
            Viewport MAIN_VP

	     /* Authorization procedure update/inquiry panel */

	    Function Response PF4_QUIT  /* User wants to stop entering   */
					/* procedures from scroll region */

		If UPD_MODE = "I" Then
		    Include RESET_CURR_PROC
		    Position Immediate To Up Occurrence
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference ONE
		    If PROCEDURE(WORD_TEMP).PROC_ID <> " " Or
		       PROCEDURE(WORD_TEMP).PROC_SVC_CODE > 0 THEN
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			Let PROCEDURE(CURR_PROC).PROC_FLAG = "D"
			Let FUNC_CODE = "D"
			Call "CEPF509AA_Scroll_Array" Using 
			    By Reference FUNC_CODE
			    By Reference PROC_ARRAY_REC
			Giving RET_VALUE
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
		    End If
		End If
		Include ENTER_PROMPT
		Call "CEPF507AA_Calc_Totals" Using
			By Reference PROC_TOTALS_REC
			By Reference PROC_ARRAY_REC
		Giving RET_VALUE

	    End Response

            Function Response BACK_FLD
		If UPD_MODE = "I" Or UPD_MODE = "U" Then 
		    Position To Previous Item
		Else
		    Include INVALID_FUNCTION
		End If
            End Response

            Function Response LEFT_ITEM
		If UPD_MODE = "I" Or UPD_MODE = "U" Then 
		    Position To Previous Item
		Else
		    Include INVALID_FUNCTION
		End If
            End Response

            Function Response RIGHT_ITEM
		If UPD_MODE = "I" Or UPD_MODE = "U" Then 
		    Position To Next Item
		Else
		    Include INVALID_FUNCTION
		End If
            End Response

	    Function Response INSERT_KEY
		If PROG_MODE = "U" Then
		    If UPD_MODE <> "S" Then
			Let MSG_ID = GEN_USEPF4ESC
			Include SHOW_MESSAGE
			Invalid
		    Else
			Let UPD_MODE = "I"
			Let FUNC_CODE = "I"
			Call "CEPF509AA_SCROLL_ARRAY" Using 
			    By Reference FUNC_CODE
			    By Reference PROC_ARRAY_REC
			Giving RET_VALUE
			If RET_VALUE = FALSE Then
			    Let MSG_ID = CEP_MAXPRCLIM
			    CALL "GENF032AA_DISPLAY_MESSAGE" Using
				By Descriptor SESSION
				By Descriptor "MESSAGE_REC"
				By Value      MSG_ID
				By Reference  SCR_COLS
				By Value      PROC_LIMIT
			    Giving RET_VALUE
			    Let UPD_MODE = "S"
			    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			    Position Immediate To 
				Field ENTER_FLD On ENTER_PANEL
			Else
			    Position Immediate 
				To Field PROCEDURE(CURR_PROC).PROC_ID 
				On AUTPROC_PANEL
			End If
		    End If
		Else
		    Let MSG_ID = GEN_INSINV
		    Include SHOW_MESSAGE
		    Invalid
		End If
	    End Response

	    Function Response REMOVE_KEY
		If PROG_MODE = "U" Then
		    If UPD_MODE <> "S" Then
			Let MSG_ID = GEN_USEPF4ESC
			Include SHOW_MESSAGE
			Invalid
		    Else
			Include CONFIRM_DELETE
			Position Immediate to Field ENTER_FLD On ENTER_PANEL
		    End If
		Else
		    Let MSG_ID = GEN_REMINV
		    Include SHOW_MESSAGE
		    Invalid
		End If

	    End Response

	    Function Response DO_KEY
		If PROG_MODE = "U" Then
		    If PROCEDURE(CURR_PROC).PROC_ID = " " And
		       PROCEDURE(CURR_PROC).PROC_SVC_CODE = -1 Then
			Let MSG_ID = CEP_USEINSKEY
			Include SHOW_MESSAGE
			Invalid
		    Else
			Let UPD_MODE = "U"
			Position Immediate 
			    To Field PROCEDURE(CURR_PROC).PROC_ID 
			    On AUTPROC_PANEL
		    End If
		Else
		    Let MSG_ID = GEN_MODNOTALL
		    Include SHOW_MESSAGE
		    Invalid
		End If
	    End Response

	    Function Response UP_ITEM
		    MESSAGE ""
		If UPD_MODE = "U" Or UPD_MODE = "I" Then
		    Let MSG_ID = CEP_FINLININP
		    Include SHOW_MESSAGE
		    Invalid
		Else
		    If CURR_PROC > 1 Then
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    	Let WORD_TEMP = CURR_PROC
		    	Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference MINUS_ONE
		    	Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    	Position To 
				Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
				On AUTPROC_PANEL
		    Else
		    	Position To Field PROCEDURE(1).PROC_ACTIVE_FLAG
				On AUTPROC_PANEL
		    	Let PROCEDURE(1).PROC_ACTIVE_FLAG = "X"
		    	Let MSG_ID = CEP_FSTPRCDSP
		    	Include SHOW_MESSAGE
		    End If
		End If
	    End Response

	    Function Response DOWN_ITEM
		    MESSAGE ""
		If UPD_MODE = "U" Or UPD_MODE = "I" Then
		    Let MSG_ID = CEP_FINLININP
		    Include SHOW_MESSAGE
		    Invalid
		Else
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference ONE
		    If PROCEDURE(WORD_TEMP).PROC_ID = " " And 
		       PROCEDURE(WORD_TEMP).PROC_SVC_CODE = -1 Then
			Let MSG_ID = CEP_LSTPRCDSP
			Include SHOW_MESSAGE
			Invalid
		    Else
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
			Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			Position To Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
		    End If
		End If
	    End Response

	    Function Response PREV_PAGE
		MESSAGE ""
		If UPD_MODE = "U" Or UPD_MODE = "I" Then
		    Let MSG_ID = CEP_FINLININP
		    Include SHOW_MESSAGE
		    Invalid
		Else
		    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    If CURR_PROC > 4 Then
			LET WORD_TEMP = CURR_PROC
			Call "GENS110AA_Calc_Word" Using
			    By Reference WORD_TEMP
			    By Reference MINUS_FOUR
			Position To Field 
			    PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    Else
			Position To Field PROCEDURE(1).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(1).PROC_ACTIVE_FLAG = "X"
			Let MSG_ID = CEP_FSTPRCDSP
			Include SHOW_MESSAGE
		    End If
		End If
	    End Response   

	    Function Response NEXT_PAGE
		MESSAGE ""
		If UPD_MODE = "U" Or UPD_MODE = "I" Then
		    Let MSG_ID = CEP_FINLININP
		    Include SHOW_MESSAGE
		    Invalid
		Else
		    LET UPD_MODE = "S"
		    Let WORD_TEMP = CURR_PROC
		    Call "GENS110AA_Calc_Word" Using
			By Reference WORD_TEMP
			By Reference FOUR
		    If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
		    	Let WORD_TEMP = CURR_PROC
		    	Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference ONE
		    	If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
			   Position To 
			      Field PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG
			      On AUTPROC_PANEL
			   Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			   Let MSG_ID = CEP_LSTPRCDSP
			   Include SHOW_MESSAGE
			ELSE
		    	   Let WORD_TEMP = CURR_PROC
		    	   Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference TWO
		    	   If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	      Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference MINUS_ONE
			      Position To 
			        Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			        On AUTPROC_PANEL
			      Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			      Let MSG_ID = CEP_LSTPRCDSP
			      Include SHOW_MESSAGE
			   ELSE
		    	      Let WORD_TEMP = CURR_PROC
		    	      Call "GENS110AA_Calc_Word" Using
				By Reference WORD_TEMP
				By Reference THREE
		    	      If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	         Call "GENS110AA_Calc_Word" Using
					By Reference WORD_TEMP
					By Reference MINUS_ONE
			         Position To 
			            Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			            On AUTPROC_PANEL
			         Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			         Let MSG_ID = CEP_LSTPRCDSP
			         Include SHOW_MESSAGE
			      ELSE
		    	         Let WORD_TEMP = CURR_PROC
		    	   	 Call "GENS110AA_Calc_Word" Using
				 	By Reference WORD_TEMP
					By Reference FOUR
		    	   	 If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 Then
		    	      	    Call "GENS110AA_Calc_Word" Using
					By Reference WORD_TEMP
					By Reference MINUS_ONE
			      	    Position To 
			        	Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			        	On AUTPROC_PANEL
			      	    Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
			            Let MSG_ID = CEP_LSTPRCDSP
			            Include SHOW_MESSAGE
				 END IF
			      END IF
			  END IF
		      END IF
		    Else
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "
			Position To 
			    Field PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG
			    On AUTPROC_PANEL
			Let PROCEDURE(WORD_TEMP).PROC_ACTIVE_FLAG = "X"
		    End If
		  End If
		End Response    

            Literal Text
                Line 2
                Column 1
                Value "                           A U T H O R I Z A T I O N"
            End Literal

            Literal Text
                Line 4
                Column 1
                Value " Authorization ID:    "
            End Literal

            Literal Text
                Line 4
                Column 30
                Value "      Type:  "
            End Literal

            Literal Text
                Line 6
                Column 1
                Value "    SSN:              Mbr:"
            End Literal

            Literal Text
                Line 8
                Column 1
                Value "    PMG/PCP: "
            End Literal

            Literal Text
                Line 9
                Column 1
                Value "    Prov Of Service:"
            End Literal

            Literal Text
                Line 11
                Column 1
                Value "    Diagnosis:"
            End Literal

            Literal Polyline
                Line 13	Column 1
                Line 13	Column 21
            End Literal

            Literal Text
                Line 13
                Column 24
                Value "P R O C E D U R E / S E R V I C E S  "
            End Literal

            Literal Polyline
                Line 13	Column 61
                Line 13	Column 80
            End Literal

            Literal Text
                Line 14
                Column 28
                Value "Occ"
            End Literal

            Literal Text
                Line 14
                Column 33
                Value "Occ"
            End Literal

            Literal Text
                Line 14
                Column 40
                Value "Amount    Amount             Authorized"
            End Literal

            Literal Text
                Line 15
                Column 2
                Value "Procedure POS  Service   Aprvd Used   Apprvd     Used "-
		      "                Date"
            End Literal

            Literal Text
                Line 21
                Column 17
                Value "Totals:  "
            End Literal

            Field HEADER_2
                Line 1
                Column 26
            End Field

            Field SCREEN_MODE
                Line 1
                Column 2
		Display
		    Bold
		Protected
            End Field

            Field AUTH_ID
                Line 4
                Column 22
		Display
		    Reverse
		Concealed when AUTH_ID = " "
		Protected

            End Field

            Field AUTH_TYPE
                Line 4
                Column 42
		Display
		    Reverse
		Concealed when AUTH_TYPE = " "
		Protected

            End Field

            Field AUTH_TYPE_DESC
                Line 4
                Column 44
		Display 
		    Reverse
		Concealed When AUTH_TYPE_DESC = " "
		Protected

            End Field

            Field AUTH_SSN
                Line 6
                Column 10
                Display
                    Reverse 
                Output Picture 999'-'99'-'9999
		Protected

            End Field

            Field AUTH_MBR_TYPE_DESC
                Line 6
                Column 28
                Display
                    Reverse      
		Concealed When AUTH_MBR_TYPE_DESC = " "
		Protected 

            End Field            

            Field AUTH_MBR_NAME
                Line 6
                Column 37
		Display
		    Reverse
		Concealed When AUTH_MBR_NAME = " "
		Protected

            End Field

            Field AUTH_PMG_ID
                Line 8
                Column 14
		Display
		    Reverse
		Concealed When AUTH_PMG_ID = " "
		Protected
            End Field

            Field AUTH_PMG_NAME
                Line 8
                Column 19
		Display
		    Reverse
		Concealed When AUTH_PMG_NAME = " "
		Protected
            End Field

            Field AUTH_PMG_NAME2
                Line 8
                Column 53
		Display
		    Reverse
		Concealed When AUTH_PMG_NAME2 = " "
		Protected
            End Field

            Field AUTH_SERV_PROV_NAME
                Line 9
                Column 22
		Display
		    Reverse
		Concealed When AUTH_SERV_PROV_NAME = " "
		Protected
            End Field

            Field AUTH_PRI_DIAG
                Line 11
                Column 17
		Display
		    Reverse
		Concealed When AUTH_PRI_DIAG = " "
		Protected
            End Field

            Field AUTH_SEC_DIAG
                Line 11
                Column 25
		Display
		    Reverse
		Concealed When AUTH_SEC_DIAG = " "
		Protected
            End Field

            Field AUTH_TER_DIAG
                Line 11
                Column 33
		Display
		    Reverse
		Concealed When AUTH_TER_DIAG = " "
		Protected
            End Field

            Group PROCEDURE

                Vertical
                    Displays 4
		    First FIRST_PROC
		    SCROLL BY PAGE

		Field PROC_ACTIVE_FLAG
		    Line 16
		    Column 1

		    Entry Response
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
		    End Response

		    Exit Response
		/*	Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = " "  */
		    End Response

		    Function Response ENTER_KEY
			Position Immediate To
			    Field ENTER_FLD On ENTER_PANEL
		    End Response

		    Function Response TAB_KEY
			Include INVALID_FUNCTION
		    End Response

		    Function Response RIGHT_ITEM
			Include INVALID_FUNCTION
		    End Response

		    Function Response LEFT_ITEM
			Include INVALID_FUNCTION
		    End Response

		    Concealed
		    No Data Input
		    Protected When PROG_MODE = "U" And UPD_MODE <> "S"

		End Field

                Field PROC_ID
                    Line 16
                    Column 2

		    Entry Response
			If UPD_MODE = "U" AND 
			   PROCEDURE(CURR_PROC).PROC_LINKED_FLAG = "*" Then
			    Let MSG_ID = CEP_CANTMODLNKPRC
			    Include SHOW_MESSAGE
			    Include ENTER_PROMPT
			Else
			    Message
				" "  
			    Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG  = "X"
			    If PROCEDURE(CURR_PROC).PROC_ID = " " And
		               PROCEDURE(CURR_PROC).PROC_SVC_CODE = -1 Then
				Let PROCEDURE(CURR_PROC).PROC_OCC_APPR     = 0
				Let PROCEDURE(CURR_PROC).PROC_OCC_USED     = 0
				Let PROCEDURE(CURR_PROC).PROC_AMT_APPR     = 0
				Let PROCEDURE(CURR_PROC).PROC_AMT_USED     = 0
				Let PROCEDURE(CURR_PROC).PROC_EST_BEN_AMT  = 0
				Let PROCEDURE(CURR_PROC).PROC_SVC_CODE     = 0
				Let PROCEDURE(CURR_PROC).PROC_FLAG	   = "S"
				Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
				Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
				Let UPD_MODE = "I"
			    Else
				Let PROCEDURE(CURR_PROC).PROC_FLAG         = "U"
				Let UPD_MODE = "U"
			    End If
			End If

		    End Response

		    Function Response LEFT_ITEM
			Let PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG = "X"
			Include INVALID_FUNCTION
		    End Response

		    Validation Response
			Let SERV_CAT = " "
			Let PROC_ID_DESC = "               "
			Let CHGS_MADE = 1
			Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
			Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
			If PROCEDURE(CURR_PROC).PROC_ID <> " " Then
			    Call "CEPS515AA_GET_RVS" Using
				By Reference PROCEDURE(CURR_PROC).PROC_ID
				By Reference SERV_CAT
				By Reference PROC_ID_DESC
			    If SERV_CAT = " " Then
				Let MSG_ID = CEP_INVPRCCDE
				Include SHOW_MESSAGE
				Invalid
			    Else
				Message
				    PROC_ID_DESC
				Let SAVE_CAT = SERV_CAT
			    End If
			Else
			    If PROCEDURE(CURR_PROC).PROC_SVC_CODE = -1 Then
				Let MSG_ID = CEP_PRCCDEREQ
				Include SHOW_MESSAGE
				Invalid
			    End If
			End If
		    End Response

		    Display
			Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"  
		    Protected When PROG_MODE = "I" Or 
				(PROG_MODE = "U" And UPD_MODE = "S")

                End Field

		Field PROC_MOD
		    Line 16
		    Column 9
		 
		    Validation Response
			If PROCEDURE(CURR_PROC).PROC_MOD <> " " Then
			    Let CHGS_MADE = 1
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
			    Let PROC_ID_DESC = "M"
			    Call "CEPS515AA_GET_RVS" Using
				By Reference PROCEDURE(CURR_PROC).PROC_MOD
				By Reference SAVE_CAT
				By Reference PROC_ID_DESC
			    If SERV_CAT <> " " Then
              /*		Message 
				    PROC_ID_DESC   This blanks procedure desc */
			    Else
				Let MSG_ID = CEP_INVPRCMOD
				Include SHOW_MESSAGE
				Invalid
			    End If
			End If

		    End Response

		    Display
			Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"  
		    Protected When PROG_MODE = "I" Or
				(PROG_MODE = "U" And UPD_MODE = "S")

		End Field

                Field PROC_POS
                    Line 16
                    Column 13

		    Function Response UP_ITEM
			Let TABLE_TYPE = "U"
			Let TABLE_ID = "PLACE_OF_SVC"
			Let TABLE_DIR = "P"
			Include SCROLL_TABLE
			Let PROCEDURE(CURR_PROC).PROC_POS = TABLE_CODE_OUT
			Message
			    TABLE_DESC
		    End Response

		    Function Response DOWN_ITEM
			Let TABLE_TYPE = "U"
			Let TABLE_ID = "PLACE_OF_SVC"
			Let TABLE_DIR = "N"
			Include SCROLL_TABLE
			Let PROCEDURE(CURR_PROC).PROC_POS = TABLE_CODE_OUT
			Message
			    TABLE_DESC
		    End Response

		    Validation Response
			If PROCEDURE(CURR_PROC).PROC_POS <> " " Then
			    Let CHGS_MADE = 1
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
			    Let TABLE_TYPE = "U"
			    Let TABLE_ID = "PLACE_OF_SVC"
			    Let TABLE_CODE = PROCEDURE(CURR_PROC).PROC_POS
			    Include VALIDATE_TABLE
			    If VALID_OPTION = FALSE Then
				Let MSG_ID = CEP_INVPRCPOS
				Include SHOW_MESSAGE
				Invalid
			    Else
				Message
				    TABLE_DESC
			    End If
			End If
			Call "CEPF502AA_Get_Service_Link" Using
			    By Reference AUTH_BEN_ADM
			    By Reference PROCEDURE(CURR_PROC).PROC_ID
			    By Reference PROCEDURE(CURR_PROC).PROC_MOD
			    By Reference PROCEDURE(CURR_PROC).PROC_POS
			    By Reference SVC_CODE_REC
			Giving RET_VALUE
			If RET_VALUE = 1 Or RET_VALUE = 2 Then
 			    Let PROCEDURE(CURR_PROC).PROC_SVC_CODE = 
				SVC_CODE_HUNDREDS
			    Let PROCEDURE(CURR_PROC).PROC_SVC_DESC = 
				SVC_CODE_HUNDREDS
			    Call "CEPF508AA_Check_Svc" Using
				By Reference PROCEDURE(CURR_PROC).PROC_SVC_DESC 
			    Giving RET_VALUE
			    Let SERV_CAT = SAVE_CAT          
			    Call "CEPS516AA_Get_Svc" Using
				By Reference PROCEDURE(CURR_PROC).PROC_SVC_DESC
				By Reference SERV_CAT
				By Reference SVC_DESCRIPTION   
			    If SVC_DESCRIPTION <> " " Then
				Let PROCEDURE(CURR_PROC).PROC_SVC_DESC = SVC_DESCRIPTION
				Position Immediate To Field 
				    PROCEDURE(CURR_PROC).PROC_OCC_APPR 
				    On AUTPROC_PANEL
			    End If 
			End If
		    End Response

		    Display	
		       Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"  
		    Protected When PROG_MODE = "I" Or
				(PROG_MODE = "U" And UPD_MODE = "S")

                End Field


                 Field PROC_SVC_CODE
                    Line 16
                    Column 16
		    RANGE 0 Thru 99
		    Input Picture 99
		    Justification Right

		    Entry Response
			If PROCEDURE(CURR_PROC).PROC_SVC_CODE < 0 Then
			    Let PROCEDURE(CURR_PROC).PROC_SVC_CODE = 0
			End If
		    End Response

		    Validation Response
			If PROCEDURE(CURR_PROC).PROC_ID = " " And
			   PROCEDURE(CURR_PROC).PROC_SVC_CODE = 0 Then
			    Let MSG_ID = CEP_SRVCDEREQ
			    Include SHOW_MESSAGE
			    Invalid
			Else
			    If PROCEDURE(CURR_PROC).PROC_SVC_CODE <> 0 Then
				Reset 
				    PROCEDURE(CURR_PROC).PROC_SVC_DESC
				    SVC_DESCRIPTION
				Let CHGS_MADE = 1
				Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = 
				    TODAY_VMSDATE
				Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = 
				    TODAY_ICIDATE
				Let SERV_CAT = SAVE_CAT
				Let PROCEDURE(CURR_PROC).PROC_SVC_DESC = 
				    PROCEDURE(CURR_PROC).PROC_SVC_CODE 
				Call "CEPF508AA_Check_Svc" Using
				    By Reference PROCEDURE(CURR_PROC).PROC_SVC_DESC 
				Giving RET_VALUE
				Call "CEPS516AA_Get_Svc" Using
				    By Reference PROCEDURE(CURR_PROC).PROC_SVC_DESC 
				    By Reference SERV_CAT
				    By Reference SVC_DESCRIPTION
				If SVC_DESCRIPTION <> " " Then
				    Let PROCEDURE(CURR_PROC).PROC_SVC_DESC = SVC_DESCRIPTION
				Else
				    Let MSG_ID = CEP_INVSRVCDE
				    Include SHOW_MESSAGE
				    Reset 
					PROCEDURE(CURR_PROC).PROC_SVC_DESC
					PROCEDURE(CURR_PROC).PROC_SVC_CODE
				    Invalid
				End If
			    End If
			End If       
		    End Response

		    Display
		       Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"  Or
			PROCEDURE(**).PROC_SVC_CODE < 0
		    Protected When PROG_MODE = "I" Or
				(PROG_MODE = "U" And UPD_MODE = "S")

                 End Field      

                 Field PROC_SVC_DESC
                    Line 16
                    Column 19

		    Protected
		    Display	
		       Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"      

                End Field      

                Field PROC_OCC_APPR
                    Line 16
                    Column 28

		    Function Response BACK_FLD
			Position Immediate To Field
			    PROCEDURE(CURR_PROC).PROC_SVC_CODE On AUTPROC_PANEL
		    End Response

		    Function Response LEFT_ITEM
			Position Immediate To Field
			    PROCEDURE(CURR_PROC).PROC_SVC_CODE On AUTPROC_PANEL
		    End Response


		    Validation Response
			If PROCEDURE(CURR_PROC).PROC_OCC_APPR <= 0 Then
			    Let MSG_ID = CEP_OCCAPRGRZ
			    Include SHOW_MESSAGE
			    Invalid
			Else
			    Let CHGS_MADE = 1
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
			End If
		    End Response

		    Input Picture  99R
                    Output Picture 9R9
		    Justification Right
		    Display	
		       Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D" Or
			PROCEDURE(**).PROC_OCC_APPR < 0
		    Protected When PROG_MODE = "I"  Or
				(PROG_MODE = "U" And UPD_MODE = "S")

                End Field

                Field PROC_OCC_USED
                    Line 16
                    Column 33
                    Output Picture 9R9
		    Display	
		       Noreverse
		    Protected
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_OCC_USED < 0 Or
			PROCEDURE(**).PROC_FLAG = "D"  

                End Field

                Field PROC_AMT_APPR
                    Line 16
                    Column 38

		    Function Response BACK_FLD
			Position Immediate To Field
			    PROCEDURE(CURR_PROC).PROC_OCC_APPR On AUTPROC_PANEL
		    End Response

		    Function Response LEFT_ITEM
			Position Immediate To Field
			    PROCEDURE(CURR_PROC).PROC_OCC_APPR On AUTPROC_PANEL
		    End Response

		    Validation Response
			If PROCEDURE(CURR_PROC).PROC_AMT_APPR <= 0 Then
			    Let MSG_ID = CEP_AMTAPRGRZ
			    Include SHOW_MESSAGE
			    Invalid
			Else
			    Let CHGS_MADE = 1
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_VMSDATE = TODAY_VMSDATE
			    Let PROCEDURE(CURR_PROC).PROC_AUTH_DATE    = TODAY_ICIDATE
			    If UPD_MODE = "I" Then
				  Let WORD_TEMP = CURR_PROC
				  Call "GENS110AA_Calc_Word" Using
					By Reference WORD_TEMP
					By Reference ONE
				  If PROCEDURE(WORD_TEMP).PROC_OCC_APPR = -1 THEN
	    			     Call "CEPF507AA_Calc_Totals" Using
					By Reference PROC_TOTALS_REC
					By Reference PROC_ARRAY_REC
					Giving RET_VALUE
				     MESSAGE ""
			       	     IF CURR_PROC = PROC_LIMIT THEN
			    	  	Let MSG_ID = CEP_MAXPRCLIM
			    	  	CALL "GENF032AA_DISPLAY_MESSAGE" Using
						By Descriptor SESSION
						By Descriptor "MESSAGE_REC"
						By Value      MSG_ID
						By Reference  SCR_COLS
						By Value  PROC_LIMIT
			    			Giving RET_VALUE
			    	  	Let UPD_MODE = "S"
			    	  	Position Immediate To Field ENTER_FLD 
						On ENTER_PANEL
  				     ELSE
				        LET 
				        PROCEDURE(CURR_PROC).PROC_ACTIVE_FLAG=" "
				     	Position To Field 
						PROCEDURE(WORD_TEMP).PROC_ID
						On AUTPROC_PANEL
				     END IF
				  Else
				     Call "CEPF507AA_CALC_TOTALS" Using
				      	By Reference PROC_TOTALS_REC
				     	By Reference PROC_ARRAY_REC
				   	Giving RET_VALUE
				     Let UPD_MODE = "S"
				     MESSAGE ""
				     Position Immediate To Field ENTER_FLD 
					On ENTER_PANEL
				  End If
			    Else
				Call "CEPF507AA_CALC_TOTALS" Using
				    By Reference PROC_TOTALS_REC
				    By Reference PROC_ARRAY_REC
				Giving RET_VALUE
				Let UPD_MODE = "S"
				Position Immediate To
				    Field ENTER_FLD On ENTER_PANEL
			    End If
			End If
		    End Response

                    Output Picture 9999R9.99
		    Justification Decimal
		    Scale -2

		    Function Response RIGHT_ITEM
			Include INVALID_FUNCTION
		    End Response

		    Display	
		       Noreverse
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_FLAG = "D"  Or
			PROCEDURE(**).PROC_AMT_APPR < 0 
		    Protected When PROG_MODE = "I" Or
				(PROG_MODE = "U" And UPD_MODE = "S")

                End Field

                Field PROC_AMT_USED
                    Line 16
                    Column 49
                    Output Picture 9999R9.99
		    Scale -2
		    Display	
		       Noreverse
		    Protected
		    Highlight
			Bold
			Reverse	
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_AMT_USED < 0 Or
			PROCEDURE(**).PROC_FLAG = "D"  

                End Field

/*              Field PROC_EST_BEN_AMT
                    Line 16
                    Column 59
                    Output Picture 9999R9.99
		    Scale -2
		    Display	
		       NoReverse
		    Protected
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X"
		    Concealed When PROCEDURE(**).PROC_EST_BEN_AMT < 0 Or
			PROCEDURE(**).PROC_FLAG = "D"  

                End Field       */

                Field PROC_AUTH_VMSDATE
                    Line 16
                    Column 69
                    Output Picture For Date NN-DD-YYYY
		    Display	
		       NoReverse
		    Protected
		    Highlight
			Bold
			Reverse	
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X" 
		    Concealed When PROCEDURE(**).PROC_AUTH_VMSDATE = "1858 11 17" Or 
				   PROCEDURE(**).PROC_FLAG = "D"  

                End Field

                Field PROC_LINKED_FLAG
                    Line 16
                    Column 79
		    Display	
		       NoReverse
		    Protected
		    Highlight
			Bold
			Reverse
			When PROCEDURE(**).PROC_ACTIVE_FLAG = "X"
		    Concealed When PROCEDURE(**).PROC_LINKED_FLAG <> "*"

                End Field

            End Group

            Field PROC_OCC_APPR_TOTAL
                Line 21
                Column 27
                Output Picture 99R9
		Display	
		   Bold
		Protected
            End Field

            Field PROC_OCC_USED_TOTAL
                Line 21
                Column 32
                Output Picture 99R9
		Display	
		   Bold
		Protected
            End Field

            Field PROC_AMT_APPR_TOTAL
                Line 21
                Column 37
                Output Picture 99999R9.99
		Scale -2
		Display	
		   Bold
		Protected

            End Field

            Field PROC_AMT_USED_TOTAL
                Line 21
                Column 48
                Output Picture 99999R9.99
		Scale -2
		Display	
		   Bold
		Protected
            End Field

/*          Field PROC_EST_BEN_AMT_TOTAL
                Line 21
                Column 58
                Output Picture 99999R9.99
		Scale -2
		Display	
		   Bold
		Protected
            End Field     */

        End Panel
    End Layout
End Form
