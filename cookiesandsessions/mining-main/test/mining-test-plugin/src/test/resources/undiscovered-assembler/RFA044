*     ***************************************************************   00001000
*     *                                                             *   00001001
*     *                           NOTICE                            *   00001002
*     *                                                             *   00001003
*     *   THIS SOFTWARE IS THE PROPERTY OF AND CONTAINS             *   00001004
*     *   CONFIDENTIAL INFORMATION OF INFOR AND/OR ITS AFFILIATES   *   00001005
*     *   OR SUBSIDIARIES AND SHALL NOT BE DISCLOSED WITHOUT PRIOR  *   00001006
*     *   WRITTEN PERMISSION. LICENSED CUSTOMERS MAY COPY AND       *   00001007
*     *   ADAPT THIS SOFTWARE FOR THEIR OWN USE IN ACCORDANCE WITH  *   00001008
*     *   THE TERMS OF THEIR SOFTWARE LICENSE AGREEMENT.            *   00001009
*     *   ALL OTHER RIGHTS RESERVED.                                *   00001010
*     *                                                             *   00001011
*     *   (C) COPYRIGHT 2017 INFOR.  ALL RIGHTS RESERVED.           *   00001012
*     *   THE WORD AND DESIGN MARKS SET FORTH HEREIN ARE            *   00001013
*     *   TRADEMARKS AND/OR REGISTERED TRADEMARKS OF INFOR          *   00001014
*     *   AND/OR ITS AFFILIATES AND SUBSIDIARIES. ALL RIGHTS        *   00001015
*     *   RESERVED.  ALL OTHER TRADEMARKS LISTED HEREIN ARE         *   00001016
*     *   THE PROPERTY OF THEIR RESPECTIVE OWNERS.                  *   00001017
*     *                                                             *   00001018
*     ***************************************************************   00001019
*     *     Infopoint Relationship CIF                              *   00001020
*     *     RF 2.1.06                                               *   00001021
*     ***************************************************************   00001022
A044     TITLE 'RFA044 - LINE DATA COMPRESS UTILITY MODULE'             00001290
RFA044   START 0                                                        00002000
RFA044   AMODE ANY                                                      00003000
RFA044   RMODE ANY                                                      00004000
         SPACE 2                                                        00013000
*---------------------------------------------------------------------* 00014000
*                                                                     * 00015000
*     RFA044 - LINE DATA COMPRESS UTILITY MODULE                      * 00016000
*                                                                     * 00017000
*     THIS ROUTINE ACCEPTS A 40 POSITION LINE OF ANY TYPE OF          * 00018000
*     DATA FROM AN APPLICATION PROGRAM, REMOVES CERTAIN               * 00019000
*     PIECES OF DATA, AND THEN COMPRESSES THE LINE.                   * 00020000
*                                                                     * 00021000
*     PARM 1          CL40         DATA AREA TO BE COMPRESSED.        * 00022000
*                                                                     * 00023000
*     PARM 2          CL200        WORKING STORAGE AREA.              * 00024000
*                                                                     * 00025000
*---------------------------------------------------------------------* 00026000
         EJECT                                                          00027000
*---------------------------------------------------------------------* 00028000
* ***    REGISTER EQUATES                                             * 00029000
*---------------------------------------------------------------------* 00030000
R0       EQU   0                                                        00031000
R1       EQU   1                       PARAMETER LIST                   00032000
R2       EQU   2                       WORKING STORAGE ADDRESS          00033000
R3       EQU   3                                                        00034000
R4       EQU   4                                                        00035000
R5       EQU   5                                                        00036000
R6       EQU   6                                                        00037000
R7       EQU   7                                                        00038000
R8       EQU   8                                                        00039000
R9       EQU   9                                                        00040000
R10      EQU   10                      BASE REGISTER 1                  00041000
R11      EQU   11                      BASE REGISTER 2                  00042000
R12      EQU   12                      BASE REGISTER 3                  00043000
R13      EQU   13                      CALL AREA SAVE ADDRESS           00044000
R14      EQU   14                      RETURN REGISTER                  00045000
R15      EQU   15                                                       00046000
         EJECT                                                          00047000
*---------------------------------------------------------------------* 00048000
* *** HOUSEKEEPING - ESTABLISH BASE REGISTERS                         * 00049000
*---------------------------------------------------------------------* 00050000
         SPACE 1                                                        00051000
         STM   R14,R12,R12(R13)        SAVE CALLERS REGISTERS           00052000
         SPACE 1                                                        00053000
CSTART   BALR  R10,0                                                    00054000
         USING CHERE,R10,R11,R12       USING BASE REGISTERS             00055000
         SPACE 1                                                        00056000
CHERE    LM    R11,R12,BASE            LOAD  BASE REGISTERS             00057000
         B     CBEGIN                                                   00058000
         SPACE 1                                                        00059000
         DC    C'RFA044.BEGIN'         PROGRAM INFORMATION              00060000
         SPACE 1                                                        00061000
BASE     DC    A(CHERE+4096,CHERE+8192)                                 00062000
         SPACE 3                                                        00063000
*---------------------------------------------------------------------* 00064000
* *** HOUSEKEEPING - LOAD UP PARAMETER, MOVE TO WORK AREA.            * 00065000
*---------------------------------------------------------------------* 00066000
         SPACE 1                                                        00067000
CBEGIN   DS    0H                                                       00068000
         L     R2,04(R1)               LOAD ADDRESS OF WORKING STORAGE  00069000
         USING WORKSTRG,R2             USING REGISTER 2 FOR W-S         00070000
         MVC   WORKBEGN,HIGHVALU       INITIALIZE W-S FIELD WORKBEGN    00071000
         MVC   WORKAREA,BLANKS         INITIALIZE W-S FIELD WORKAREA    00072000
         MVC   WORKEND,BLANKS          INITIALIZE W-S FIELD WORKEND     00073000
         MVC   WORKTRAL,BLANKS         INITIALIZE W-S FIELD WORKTRAL    00074000
         SPACE 1                                                        00075000
         L     R3,0(R1)                LOAD ADDRESS OF DATA AREA        00076000
         CLC   0(40,R3),BLANKS         Q. IS DATA SENT BLANK?           00077000
         BE    RETURN99                A. YES, RETURN WITH NO CHANGES   00078000
         MVC   WORKAREA(40),0(R3)      MOVE DATA TO WORK AREA           00079000
         TR    WORKAREA(40),TR$TABLE   REMOVE ALL INVALID CHARACTERS    00080000
         MVC   WORKEND(2),=X'FF40'     SET END OF DATA AREA.            00081000
         LA    R4,WORKAREA             INITIALIZE REGISTER 4            00082000
         EJECT                                                          00083000
*---------------------------------------------------------------------* 00084000
* *** PASS DATA AREA, REMOVING UNNEEDED DATA                          * 00085000
*---------------------------------------------------------------------* 00086000
         SPACE 1                                                        00087000
TABLE$LP DS    0H                                                       00088000
         CLI   0(R4),X'FF'             Q. AT END OF THE DATA AREA?      00089000
         BE    COMPRESS                A. YES, GO COMPRESS THE DATA     00090000
         CLI   0(R4),C'-'              Q. IS THIS A DASH?               00091000
         BNE   TABLE$L5                A. NO, GO CHECK FOR MORE CHARS   00092000
         SH    R4,=H'1'                A. YES, LOOK AT PREVIOUS POSITIN 00093000
         CLI   0(R4),X'40'             Q. IS PREV POS A SPACE?          00094000
         BNE   TABLE$L1                A. NO, GO PROCESS AS ENDING DASH 00095000
         MVC   0(2,R4),=C'- '          A. YES, REVRSE POS OF SPACE/DASH 00096000
         B     TABLE$LP                LOOP TO CHECK DASH CODE          00097000
         SPACE 1                                                        00098000
TABLE$L1 DS    0H                                                       00099000
         LA    R4,1(R4)                RESET TO DASH POSITION           00100000
         CLI   1(R4),C'-'              Q. IS NEXT POSITION A DASH?      00101000
         BE    TABLE$L3                A. YES, DELETE NEXT POSITION     00102000
         CLI   1(R4),X'40'             Q. IS NEXT POSITION A SPACE?     00103000
         BNE   BUMPIT                  A. NO, GO BUMP TO NEXT POSITION  00104000
         SPACE 1                                                        00105000
TABLE$L3 DS    0H                                                       00106000
         MVC   1(41,R4),2(R4)          BUMP OVER NEXT POSITION          00107000
         B     TABLE$LP                LOOP TO LOOK AT DASH AGAIN       00108000
         SPACE 1                                                        00109000
TABLE$L5 DS    0H                                                       00110000
         CLI   0(R4),X'7F'             Q. IS THIS A DOUBLE QUOTE?       00113000
         BE    TABLE$L7                A. YES, SCRUNCH OVER THE DATA    00114000
         B     TABLE$L8                A. NO, GO CHECK FOR SPEC CHARS   00115000
         SPACE 1                                                        00116000
TABLE$L7 DS    0H                                                       00117000
         MVC   0(41,R4),1(R4)          BUMP OVER THIS POSITION          00118000
         B     TABLE$LP                LOOP TO LOOK AT POSITION AGAIN   00119000
         SPACE 1                                                        00120000
TABLE$L8 DS    0H                                                       00121000
         SPACE 1                                                        00122000
BUMPIT   DS    0H                                                       00123000
         LA    R4,1(R4)                BUMP TO NEXT DATA POSITION       00124000
         B     TABLE$LP                GO CHECK NEXT POSITION           00125000
         SPACE 1                                                        00126000
DELETEIT DS    0H                                                       00127000
         MVI   0(R4),X'40'             BLANK OUT SPECIAL CHARACTER      00128000
         B     TABLE$LP                GO CHECK NEXT POSITION           00129000
         EJECT                                                          00130000
*---------------------------------------------------------------------* 00131000
* **  COMPRESS THE DATA, REMOVING EXTRA BLANKS                        * 00132000
*---------------------------------------------------------------------* 00133000
         SPACE 1                                                        00134000
COMPRESS DS    0H                                                       00135000
         LA    R4,WORKAREA             RESET REG 4 TO WORK AREA         00136000
         SPACE 1                                                        00137000
COMPRESA DS    0H                                                       00138000
         CLI   0(R4),X'FF'             Q. AT END OF THE DATA AREA?      00139000
         BE    RETURN                  A. YES, GO RETURN TO CALLER      00140000
         CLI   0(R4),X'40'             Q. IS FIRST POS OF LINE BLANK?   00141000
         BNE   COMPRESL                A. NO, GO BUMP TO NEXT POSITION  00142000
         MVC   0(41,R4),1(R4)          A. YES, OFFSET DATA TO LEFT      00143000
         B     COMPRESA                GO TRY FOR ANOTHER BLANK         00144000
         SPACE 1                                                        00145000
COMPRESL DS    0H                                                       00146000
         CLI   0(R4),X'FF'             Q. AT END OF THE DATA AREA?      00147000
         BE    RETURN                  A. YES, GO RETURN TO CALLER      00148000
         CLI   0(R4),X'40'             Q. IS THIS A BLANK THIS POS?     00149000
         BNE   LOOPIT                  A. NO, GO BUMP TO NEXT POSITION  00150000
         CLI   1(R4),X'40'             Q. IS THIS A BLANK THIS POS?     00151000
         BNE   LOOPIT                  A. NO, GO BUMP TO NEXT POSITION  00152000
         MVC   0(41,R4),1(R4)          A. YES, OFFSET DATA TO LEFT      00153000
         B     COMPRESL                GO TRY FOR ANOTHER BLANK         00154000
         SPACE 1                                                        00155000
LOOPIT   DS    0H                                                       00156000
         LA    R4,1(R4)                BUMP TO NEXT DATA POSITION       00157000
         B     COMPRESL                AND TRY AGAIN                    00158000
         EJECT                                                          00159000
*---------------------------------------------------------------------* 00160000
* *** RETURN TO CALLING PROGRAM                                       * 00161000
*---------------------------------------------------------------------* 00162000
         SPACE 1                                                        00163000
RETURN   DS    0H                                                       00164000
         MVC   0(40,R4),BLANKS         CLEAR OUT POSSIBLE END FLAG      00165000
         MVC   0(40,R3),WORKAREA       MOVE DATA TO OUTPUT AREA         00166000
         SPACE 1                                                        00167000
RETURN99 DS    0H                                                       00168000
         LM    R14,R12,R12(R13)        RESTORE REGISTERS                00169000
         BR    R14                     RETURN                           00170000
         EJECT                                                          00171000
*---------------------------------------------------------------------* 00172000
* *** WORKING STORAGE                                                 * 00173000
*---------------------------------------------------------------------* 00174000
         SPACE 1                                                        00175000
HIGHVALU DC    XL04'FFFFFFFF'          HIGH VALUE AREA                  00176000
BLANKS   DC    CL80' '                 BLANKS FOR COMPARE               00177000
         SPACE 3                                                        00178000
*---------------------------------------------------------------------* 00179000
* *** LITERAL POOL                                                    * 00180000
*---------------------------------------------------------------------* 00181000
         LTORG                                                          00182000
         EJECT                                                          00183000
*---------------------------------------------------------------------* 00184000
* *** TRANSLATE TABLE FOR INVALID CHARACTERS                          * 00185000
*---------------------------------------------------------------------* 00186000
         SPACE 1                                                        00187000
TR$TABLE DS    0CL256                  TR TABLE FOR INVALID CHARACTERS  00188000
         DC    XL16'40404040404040404040404040404040'   00-0F           00189000
         DC    XL16'40404040404040404040404040404040'   10-1F           00190000
         DC    XL16'40404040404040404040404040404040'   20-2F           00191000
         DC    XL16'40404040404040404040404040404040'   30-3F           00192000
         DC    XL16'404042434445464040494A404C4D4E4F'   40-4F 4B=PERIOD 00193000
         DC    XL16'505152535455565758405A5B5C5D5E5F'   50-5F           00193590
         DC    XL16'606140404040404040406A406C6D6E6F'   60-6F 6B=COMMA  00195000
         DC    XL16'40404040404040404079407B7C7D7E7F'   70-7F 7A=SMICLN 00196000
         DC    XL16'40818283848586878889408B8C8D8E8F'   80-8F           00197000
         DC    XL16'40919293949596979899409B9C9D9E9F'   90-9F           00198000
         DC    XL16'A0A1A2A3A4A5A6A7A8A940ABACADAEAF'   A0-AF           00199000
         DC    XL16'B0B1B2B3B4B5B6B7B8B940BBBCBDBEBF'   B0-BF           00200000
         DC    XL16'C0C1C2C3C4C5C6C7C8C940CBCCCDCECF'   C0-CF           00201000
         DC    XL16'D0D1D2D3D4D5D6D7D8D940DBDCDDDE40'   D0-DF           00201590
         DC    XL16'E040E2E3E4E5E6E7E8E94040EC404040'   E0-EF           00203000
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FA4040404040'   F0-FF           00204000
         SPACE 3                                                        00205000
         DC    C'RFA044.V21M00'         PROGRAM VERSION INFO            00206000
         EJECT                                                          00207000
*---------------------------------------------------------------------* 00208000
* *** WORKING STORAGE DSECT                                           * 00209000
*---------------------------------------------------------------------* 00210000
         SPACE 1                                                        00211000
WORKSTRG DSECT                         WORKING STORAGE AREA             00212000
WORKBEGN DS    CL02                    INDICATES BEGIN OF WORK AREA     00213000
WORKAREA DS    CL40                    WORK AREA FOR DATA LINE          00214000
WORKEND  DS    CL50                    ENDING WORK AREA BLANKS          00215000
WORKTRAL DS    CL80                    TRAILING W-S AREA                00216000
         END                                                            00217000
