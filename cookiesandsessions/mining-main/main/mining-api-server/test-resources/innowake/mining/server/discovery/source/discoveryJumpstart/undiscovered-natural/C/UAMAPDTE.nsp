/* UAMAPDTE * Exportieren eines Datentransferauftrags auf PC      /* RSE        
/*          *       
DEFINE DATA         
 GLOBAL USING UAMGDA01
  LOCAL     USING UAMAADTP      
  LOCAL     USING UAMAADPG      
  LOCAL 
    1 UAMDTH VIEW OF UAMPDATENTRANSFERPUFFER
      2 UAMP-DT-KEY 
  LOCAL 
    1 #VON-KEY            (A31) 
    1 REDEFINE #VON-KEY         
      2 #VKEY-SA          (A1)  
      2 #VKEY-TYP         (A1)  
      2 #VKEY-RA          (N5)  
      2 #VKEY-MD          (N5)  
      2 #VKEY-UMD         (N3)  
      2 #VKEY-AUFTRAG     (N8)  
      2 #VKEY-LFD-NR      (N8)  
  LOCAL 
    1 #BIS-KEY            (A31) 
    1 REDEFINE #BIS-KEY         
      2 #BKEY-SA          (A1)  
      2 #BKEY-TYP         (A1)  
      2 #BKEY-RA          (N5)  
      2 #BKEY-MD          (N5)  
      2 #BKEY-UMD         (N3)  
      2 #BKEY-AUFTRAG     (N8)  
      2 #BKEY-LFD-NR      (N8)  
  LOCAL 
    1 HILFSVARIABLEN
      2 #FOUND            (L)   
      2 #COMAREA          (A80) 
END-DEFINE          
/*-------------------------------------------------- Initialisierung ---        
RESET G-UAMRC(11)   
/*      
CALLNAT 'UAMZNPON'  
/*----------------------------------------- Auftragsrahmen ermitteln ---        
CALLNAT 'UAMANDTP'  
  G-UAMMF (*) G-UAMINFO G-UAMTITEL G-UAMF1 G-UAMF2      
  G-UAMPFT(*) G-UAMPF1(*) G-UAMPFB(*) UAMGDA1(*) G-UAMPARM(*)       
  UAMAADTP(*)       
/*      
IF G-UAMRC(11) NE 0 THEN        
  COMPRESS 'Der Datenexport wurde mit Fehler' G-UAMRC(11) 'abgebrochen!'        
    INTO G-UAMREINPUT           
  ESCAPE ROUTINE IMMEDIATE      
END-IF  
/*------------------------------------------ Sperrkennzeichen prüfen ---        
IF UAMAADTP.#AFT-SPERR-KNZ EQ 'J' THEN      
  COMPRESS 'Der Datenexport wurde wegen gesperrter Auftragsfunktion'
    'abgebrochen!' INTO G-UAMREINPUT        
  ESCAPE ROUTINE IMMEDIATE      
END-IF  
/*      
IF UAMAADTP.#ZIEL-SPERR-KNZ EQ 'J' THEN     
  COMPRESS 'Der Datenexport wurde wegen gesperrtem Ziel abgebrochen!'           
    INTO G-UAMREINPUT           
  ESCAPE ROUTINE IMMEDIATE      
END-IF  
/*      
IF NOT(UAMAADTP.#ZIEL-BER EQ 'P' OR= 'A') THEN          
  COMPRESS 'Der Datenexport wurde wegen unberechtigtem Ziel'        
    'abgebrochen!' INTO G-UAMREINPUT        
  ESCAPE ROUTINE IMMEDIATE      
END-IF  
/*--------------------------------------- Vorhandene Einträge prüfen ---        
RESET  #VON-KEY     
ASSIGN #VKEY-SA      = 'T'      
ASSIGN #VKEY-TYP     = UAMAADTP.TYP         
ASSIGN #VKEY-RA      = UAMAADTP.RA          
ASSIGN #VKEY-MD      = UAMAADTP.MD          
ASSIGN #VKEY-UMD     = UAMAADTP.UMD         
ASSIGN #VKEY-AUFTRAG = UAMAADTP.AUFTRAG     
ASSIGN #VKEY-LFD-NR  = 0        
ASSIGN #BIS-KEY      = #VON-KEY 
ASSIGN #BKEY-LFD-NR  = 99999999 
/*      
RESET  #FOUND       
HISTOGRAM(1) UAMDTH UAMP-DT-KEY 
  STARTING FROM #VON-KEY ENDING AT #BIS-KEY 
  ASSIGN #FOUND = TRUE          
END-HISTOGRAM       
/*      
IF NOT(#FOUND) THEN 
  COMPRESS 'Der Datenexport wurde wegen fehlender Einträge abgebrochen!'        
    INTO G-UAMREINPUT           
  ESCAPE ROUTINE IMMEDIATE      
END-IF  
/*------------------------------------------------- Status schreiben ---        
ASSIGN UAMAADPG.CMD = 'EA'      
ASSIGN UAMAADPG.TYP = UAMAADTP.TYP          
ASSIGN UAMAADPG.MD  = UAMAADTP.MD           
ASSIGN UAMAADPG.UMD = UAMAADTP.UMD          
ASSIGN UAMAADPG.NR  = UAMAADTP.AUFTRAG      
/*      
CALLNAT 'UAMANDPG'  
  G-UAMMF(*)  G-UAMINFO   G-UAMTITEL  G-UAMF1    G-UAMF2
  G-UAMPFT(*) G-UAMPF1(*) G-UAMPFB(*) UAMGDA1(*) G-UAMPARM(*)       
  UAMAADPG #COMAREA 
/*----------------------------------------------------------------------        
END
