DEFINE DATA
LOCAL USING Z1069--A
LOCAL USING Z1069--L

LOCAL
1 VIEW1 VIEW OF DDM1687A
  2 AAA
  2 BBB

1 VIEW2 VIEW OF DDM1687B
  2 AAA
  2 BBB
  
1 VIEW3 VIEW OF DDM1687C
  2 AAA
  
* total count for display only
1 #TESTCASE (I4)

* view for store 1..3
1 #STORE (I4)

* view for update 1..3
1 #UPDATE (I4)

* type of update 7..9: A, B, A+B
1 #UPDATE-VARIANT (I4)

* view for read 1..3
1 #READ (I4)

* view for delete 1..3
1 #DELETE (I4)

* isn of newly created record
1 #ISN (P10)

END-DEFINE

INCLUDE Z1069--C
FORMAT (1) PS=60 LS=131

FOR #STORE = 1 TO 3
	FOR #UPDATE = 1 TO 3
		FOR #UPDATE-VARIANT = 7 TO 9
			FOR #READ = 1 TO 2
				FOR #DELETE = 1 TO 3
* view3 only has one field AAA and no descriptor, so when updating view 3 only variant 7 does make sense
					IF #UPDATE NE 3 OR #UPDATE-VARIANT EQ 7 THEN
						NEWPAGE(1)
						ADD 1 TO #TESTCASE
						DISPLAY (1) NOTITLE #TESTCASE #STORE #UPDATE #UPDATE-VARIANT #READ #DELETE
						PERFORM CLEAR
						PERFORM STORE
						PERFORM UPDATE
						PERFORM READ
						PERFORM DELETE
					END-IF
				END-FOR
			END-FOR
		END-FOR
	END-FOR
END-FOR

* clear entire database table
DEFINE SUBROUTINE CLEAR
	READ VIEW1 BY ISN
		DELETE
	END-READ
END-SUBROUTINE

* perform store either on view1 or view2 or view3
DEFINE SUBROUTINE STORE
	DECIDE ON FIRST VALUE OF #STORE
		VALUE 1
			RESET VIEW1
			MOVE 'AAA' TO VIEW1.AAA
			MOVE 'BBB' TO VIEW1.BBB
			STORE1. STORE VIEW1
			#ISN := *ISN(STORE1.)
		VALUE 2
			RESET VIEW2
			MOVE 'YYY' TO VIEW2.AAA
			MOVE 'ZZZ' TO VIEW2.BBB
			STORE2. STORE VIEW2
			#ISN := *ISN(STORE2.)
		VALUE 3
			RESET VIEW3
			MOVE 'HHH' TO VIEW3.AAA
			STORE3. STORE VIEW3
			#ISN := *ISN(STORE3.)
		NONE VALUE
			IGNORE
	END-DECIDE
END-SUBROUTINE

* get with either view1 or view2 or view3 and perform either update A or B or A+B
DEFINE SUBROUTINE UPDATE
	DECIDE ON FIRST VALUE OF #UPDATE
		VALUE 1
			RESET VIEW1
			GET VIEW1 #ISN
			DECIDE ON FIRST VALUE OF #UPDATE-VARIANT
				VALUE 7
					MOVE 'AAA' TO VIEW1.AAA
				VALUE 8
					MOVE 'BBB' TO VIEW1.BBB
				VALUE 9
					MOVE 'AAA' TO VIEW1.AAA
					MOVE 'BBB' TO VIEW1.BBB
				NONE VALUE
					IGNORE
			END-DECIDE
			UPDATE
		VALUE 2
			RESET VIEW2
			GET VIEW2 #ISN
			DECIDE ON FIRST VALUE OF #UPDATE-VARIANT
				VALUE 7
					MOVE 'YYY' TO VIEW2.AAA
				VALUE 8
					MOVE 'ZZZ' TO VIEW2.BBB
				VALUE 9
					MOVE 'YYY' TO VIEW2.AAA
					MOVE 'ZZZ' TO VIEW2.BBB
				NONE VALUE
					IGNORE
			END-DECIDE
			UPDATE
		VALUE 3
			RESET VIEW3
			GET VIEW3 #ISN
			DECIDE ON FIRST VALUE OF #UPDATE-VARIANT
				VALUE 7
					MOVE 'HHH' TO VIEW3.AAA
* 				VALUE 8
* 					MOVE 'ZZZ' TO VIEW3.BBB
* 				VALUE 9
* 					MOVE 'YYY' TO VIEW3.AAA
* 					MOVE 'ZZZ' TO VIEW3.BBB
				NONE VALUE
					IGNORE
			END-DECIDE
			UPDATE
		NONE VALUE
			IGNORE
	END-DECIDE
END-SUBROUTINE

* read either with view1 or view2 with descriptor definied in view (either BBB or AAA)
DEFINE SUBROUTINE READ
	DECIDE ON FIRST VALUE OF #READ
		VALUE 1
			RESET VIEW1
			READ VIEW1 BY ISN 
  				WRITE (1) NOTITLE 'by isn' VIEW1.AAA VIEW1.BBB
			END-READ
			
			RESET VIEW1
			READ VIEW1 LOGICAL BY BBB
  				WRITE (1) NOTITLE 'by dsc' VIEW1.AAA VIEW1.BBB
			END-READ
		VALUE 2		
			RESET VIEW2
			READ VIEW2 BY ISN
  				WRITE (1) NOTITLE 'by isn' VIEW2.AAA VIEW2.BBB
			END-READ
			
			RESET VIEW2
			READ VIEW2 LOGICAL BY AAA
  				WRITE (1) NOTITLE 'by dsc' VIEW2.AAA VIEW2.BBB
			END-READ
		VALUE 3
			RESET VIEW3
			READ VIEW3 BY ISN
  				WRITE (1) NOTITLE 'by isn' VIEW3.AAA
			END-READ
* cannot read by descriptor as view3 has none
		NONE VALUE
			IGNORE
	END-DECIDE
END-SUBROUTINE

* delete either with view1 or view2 or view3
DEFINE SUBROUTINE DELETE
	DECIDE ON FIRST VALUE OF #DELETE
		VALUE 1
			RESET VIEW1
			GET VIEW1 #ISN
			DELETE
		VALUE 2
			RESET VIEW2
			GET VIEW2 #ISN
			DELETE
		VALUE 3
			RESET VIEW3
			GET VIEW3 #ISN
			DELETE
		NONE VALUE
			IGNORE
	END-DECIDE
END-SUBROUTINE

END
