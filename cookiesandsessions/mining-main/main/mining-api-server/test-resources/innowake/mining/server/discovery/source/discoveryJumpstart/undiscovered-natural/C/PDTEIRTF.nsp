/***********************************************************************        
/* PDTEIRTF - üBERLEITUNG VON KONTENERöFFNUNGEN UND BUCHUNGEN       
/*            NACH RTF FüR 'AUTONOME' PROCOM-KUNDEN     
/* ERSTELLT VON: VI 05/95       
/* GEäNDERT VON:    
/***********************************************************************        
DEFINE DATA         
 GLOBAL USING UAMGDA01
LOCAL USING LDTEIRTF                    /* DATENCONTAINER           
/***********************************************************************        
LOCAL USING UAMAADTA
LOCAL   
/************** INPUT **************************************************        
1 #PARM                 (A80/20)
1 REDEFINE #PARM    
2 #DUMMY                (20)    
3 #PARM-MANDANT         (N5)    
3 #PARM-UNTERMANDANT    (N3)    
3 #PARM-REST            (A72)   
/***********************************************************************        
1 #COMAREA    (A80) 
1 #I          (I1)  
/*      
1 #FEHLER                  (L)  
1 #FEHLER-MD               (N5) 
1 #FEHLER-UMD              (N3) 
1 #FEHLER-AUFTRAG          (N8) 
1 #FEHLER-AUFTRAGSFUNKTION (A12)
END-DEFINE          
/*      
/****************** INITIALISERING *************************************        
ASSIGN G-UAMBETRIEB = '2'       
ASSIGN G-UAMREFAPPL = 1         
/*      
/****************** OUTPUT *********************************************        
FORMAT LS=81        
INPUT #PARM(1)      
INPUT #PARM(2)      
INPUT #PARM(3)      
INPUT #PARM(4)      
INPUT #PARM(5)      
INPUT #PARM(6)      
INPUT #PARM(7)      
INPUT #PARM(8)      
INPUT #PARM(9)      
INPUT #PARM(10)     
INPUT #PARM(11)     
INPUT #PARM(12)     
INPUT #PARM(13)     
INPUT #PARM(14)     
INPUT #PARM(15)     
INPUT #PARM(16)     
INPUT #PARM(17)     
INPUT #PARM(18)     
INPUT #PARM(19)     
INPUT #PARM(20)     
/*      
/*************** INITIALISIERUNG ***************************************        
ASSIGN UAMAADTA.TYP = 'U' /* <--- UNTERMANDANTENAUFTRAG 
ASSIGN UAMAADTA.FILTER-STATUS(1) = '4' /* <-- ALLE üBERTRAGEN GEPLANT           
ASSIGN UAMAADTA.FILTER-STATUS(2) = '5' /* <-- ALLE üBERTRAGEN AKTIV 
ASSIGN UAMAADTA.FILTER-STATUS(3) = '7' /* <-- ALLE FEHLERHAFTEN NOCHMALS        
/*      
/*************** SäTZE AUS BASIS HOLEN *********************************        
FOR #I = 1 TO 20    
  IF #PARM-MANDANT(#I) = 0 OR #PARM-UNTERMANDANT(#I) = 0 THEN       
    ESCAPE TOP      
  END-IF
/*      
  ASSIGN UAMAADTA.MD  = #PARM-MANDANT(#I)   
  ASSIGN UAMAADTA.UMD = #PARM-UNTERMANDANT(#I)          
/*      
/*--------------- KONTENERöFFNUNGEN ------------------------------------        
  ASSIGN UAMAADTA.CMD = 'FQ'       /* <-- ERSTEN AUFTRAG LESEN      
  ASSIGN UAMAADTA.FILTER-AUFTRAGSFUNKTION = 'IKMUX-RTF-KE'          
/*      
  REPEAT
    PERFORM UAMANDTA
    IF UAMAADTA.RC > 0          
      ESCAPE BOTTOM 
    END-IF          
/*      
    PERFORM SAETZE-LESEN           /* <-- SäTZE DES AUFTRAGS LESEN  
    ASSIGN UAMAADTA.CMD = 'US'     /* <-- STATUS SETZEN 
    ASSIGN UAMAADTA.STATUS = '5'   /* <-- üBERTRAGEN IST AKTIV      
    PERFORM UAMANDTA
    INCLUDE UAMCCETO               /* <-- AUFTRAG BERARBEITET       
/*      
    ASSIGN UAMAADTA.CMD = '+Q'       /* <-- NäCHSTEN AUFTRAG        
  END-REPEAT        
/*      
/*--------------- BUCHUNGEN --------------------------------------------        
  ASSIGN UAMAADTA.CMD = 'FQ'       /* <-- ERSTEN AUFTRAG LESEN      
  ASSIGN UAMAADTA.FILTER-AUFTRAGSFUNKTION = 'IKMUX-RTF-KB'          
/*      
  REPEAT
    PERFORM UAMANDTA
    IF UAMAADTA.RC > 0          
      ESCAPE BOTTOM 
    END-IF          
/*      
    PERFORM SAETZE-LESEN             /* <-- SäTZE DES AUFTRAGES LESEN           
    ASSIGN UAMAADTA.CMD = 'US'       /* <-- STATUS SETZEN           
    ASSIGN UAMAADTA.STATUS = '5'     /* <-- üBERTRAGEN IST AKTIV    
    PERFORM UAMANDTA
    INCLUDE UAMCCETO                 /* <-- AUFTRAG BERARBEITET     
/*      
    ASSIGN UAMAADTA.CMD = '+Q'       /* <-- NäCHSTEN AUFTRAG        
  END-REPEAT        
/*      
END-FOR 
/*      
/***************** üBERTRAGUNG KORREKT *********************************        
PERFORM STATUS-SETZEN           
INCLUDE UAMCCETO                 /* <-- AUFTRäGE BEARBEITET         
/*      
/***************** üBERTRAGUNG FEHLERHAFT ******************************        
ON ERROR
  ASSIGN #FEHLER = TRUE         
  ASSIGN #FEHLER-MD = UAMAADTA.MD           
  ASSIGN #FEHLER-UMD = UAMAADTA.UMD         
  ASSIGN #FEHLER-AUFTRAG = UAMAADTA.AUFTRAG 
  ASSIGN #FEHLER-AUFTRAGSFUNKTION = UAMAADTA.AUFTRAGSFUNKTION       
  PERFORM STATUS-SETZEN         
  INCLUDE UAMCCETO               /* <-- AUFTRäGE FEHLERHAFT         
  TERMINATE 32      
END-ERROR           
/*      
/***********************************************************************        
/* SAETZE-LESEN - LIEST DIE DATENSäTZE üBER DEFINIERTE SCHNITTSTELLE
/***********************************************************************        
DEFINE SUBROUTINE SAETZE-LESEN  
/*      
ASSIGN UAMAADTA.CMD = 'FD'          /* <-- ERSTEN DATENSATZ LESEN   
REPEAT  
  PERFORM UAMANDTA  
  IF UAMAADTA.RC > 0
    ESCAPE BOTTOM   
  END-IF
/*      
  WRITE WORK FILE 1 #RTF-SAETZE   /* <-- DATENSATZ SCHREIBEN        
  ASSIGN CMD = '+D'               /* <-- NäCHSTEN DATENSATZ LESEN   
/*      
END-REPEAT          
/*      
END-SUBROUTINE      
/*      
/***********************************************************************        
/* STATUS-SETZEN    
/***********************************************************************        
DEFINE SUBROUTINE STATUS-SETZEN 
/*      
ASSIGN UAMAADTA.TYP = 'U'              /* <-- UNTERMANDANTENAUFTRAG 
ASSIGN UAMAADTA.FILTER-STATUS(1) = '5' /* <-- ALLE üBERTRAGEN AKTIV 
/*      
FOR #I = 1 TO 20    
  IF #PARM-MANDANT(#I) = 0 OR #PARM-UNTERMANDANT(#I) = 0 THEN       
    ESCAPE TOP      
  END-IF
/*      
  ASSIGN UAMAADTA.MD  = #PARM-MANDANT(#I)   
  ASSIGN UAMAADTA.UMD = #PARM-UNTERMANDANT(#I)          
/*      
/*---------------- KONTENERöFFNUNGEN -----------------------------------        
  ASSIGN UAMAADTA.CMD = 'FQ'       /* <-- ERSTEN AUFTRAG LESEN      
  ASSIGN UAMAADTA.FILTER-AUFTRAGSFUNKTION = 'IKMUX-RTF-KE'          
/*      
  REPEAT
    PERFORM UAMANDTA
    IF UAMAADTA.RC > 0          
      ESCAPE BOTTOM 
    END-IF          
/*      
    ASSIGN UAMAADTA.CMD = 'US'       /* <-- STATUS SETZEN           
    IF #FEHLER      
      ASSIGN UAMAADTA.STATUS = '7'   /* <-- EIN FEHLER IST AUFGETRETEN          
    ELSE
      ASSIGN UAMAADTA.STATUS = '6'   /* <-- BEARBEITUNG OK          
    END-IF          
    PERFORM UAMANDTA
/*      
    IF #FEHLER-MD NE 0 AND #FEHLER-UMD NE 0 
       AND #FEHLER-MD = #PARM-MANDANT(#I)   
       AND #FEHLER-UMD = #PARM-UNTERMANDANT(#I)         
       AND #FEHLER-AUFTRAG = UAMAADTA.AUFTRAG           
       AND #FEHLER-AUFTRAGSFUNKTION = UAMAADTA.AUFTRAGSFUNKTION     
      ESCAPE ROUTINE
    END-IF          
/*      
    ASSIGN UAMAADTA.CMD = '+Q'       /* <-- NäCHSTEN AUFTRAG LESEN  
  END-REPEAT        
/*      
/*--------------------- BUCHUNGEN --------------------------------------        
  ASSIGN UAMAADTA.CMD = 'FQ'       /* <-- ERSTEN AUFTRAG LESEN      
  ASSIGN UAMAADTA.FILTER-AUFTRAGSFUNKTION = 'IKMUX-RTF-KB'          
/*      
  REPEAT
    PERFORM UAMANDTA
    IF UAMAADTA.RC > 0          
      ESCAPE BOTTOM 
    END-IF          
/*      
    ASSIGN UAMAADTA.CMD = 'US'       /* <-- STATUS SETZEN           
    IF #FEHLER      
      ASSIGN UAMAADTA.STATUS = '7'   /* <-- EIN FEHLER IST AUFGETRETEN          
    ELSE
      ASSIGN UAMAADTA.STATUS = '6'   /* <-- BEARBEITUNG OK          
    END-IF          
    PERFORM UAMANDTA
/*      
    IF #FEHLER-MD NE 0 AND #FEHLER-UMD NE 0 
       AND #FEHLER-MD = #PARM-MANDANT(#I)   
       AND #FEHLER-UMD = #PARM-UNTERMANDANT(#I)         
       AND #FEHLER-AUFTRAG = UAMAADTA.AUFTRAG           
       AND #FEHLER-AUFTRAGSFUNKTION = UAMAADTA.AUFTRAGSFUNKTION     
      ESCAPE ROUTINE
    END-IF          
/*      
    ASSIGN UAMAADTA.CMD = '+Q'       /* <-- NäCHSTEN AUFTRAG LESEN  
  END-REPEAT        
/*      
END-FOR 
/*      
END-SUBROUTINE      
/*      
/***********************************************************************        
/* UAMANDTA - DATENTRANSFERMANAGER AUFRUFEN 
/***********************************************************************        
DEFINE SUBROUTINE UAMANDTA      
/*      
CALLNAT 'UAMANDTA' G-UAMMF (*) G-UAMINFO G-UAMTITEL G-UAMF1 G-UAMF2 
                   G-UAMPFT(*) G-UAMPF1(*) G-UAMPFB(*) UAMGDA1(*)   
                   G-UAMPARM(*) 
                   UAMAADTA LDTE #COMAREA   
/*      
IF UAMAADTA.RC < 0  
  ASSIGN #FEHLER = TRUE         
  ASSIGN #FEHLER-MD = UAMAADTA.MD           
  ASSIGN #FEHLER-UMD = UAMAADTA.UMD         
  ASSIGN #FEHLER-AUFTRAG = UAMAADTA.AUFTRAG 
  ASSIGN #FEHLER-AUFTRAGSFUNKTION = UAMAADTA.AUFTRAGSFUNKTION       
  PERFORM STATUS-SETZEN         
  INCLUDE UAMCCETO  
  TERMINATE 32      
END-IF  
/*      
END-SUBROUTINE      
/*      
END     
