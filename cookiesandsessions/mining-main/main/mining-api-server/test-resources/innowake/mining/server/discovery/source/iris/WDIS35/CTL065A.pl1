 /* Positive Test case to demonstrate usage of list attribute in Entry statement
    Pass BYVALUE
    Here we are seperating String and Parameters and looping till the last 
    parameter and printing parameters. */
 
 CTL065A: PROC OPTIONS(MAIN);
    
    DCL I1 FIXED BIN(31) INIT(1729);
    DCL I2 FIXED BIN(31) INIT(6);
    DCL D1 FLOAT BIN(53) INIT(17.29);

    DCL VARG1 ENTRY( CHAR(*) VARZ BYADDR LIST )
               OPTIONS(BYVALUE NODESCRIPTOR );

    CALL VARG1( 'TEST BYVALUE' );
    CALL VARG1( 'TEST1 PARM1=%I', I1 );
    CALL VARG1( 'TEST2 PARM1=%I PARM2=%I', I1, I2 );
    CALL VARG1( 'TEST3 PARM1=%D', D1 );
    CALL VARG1( 'TEST4 PARM1=%I PARM2=%I DEMO TEXT', I1, I2 );
 
 END CTL065A;

 VARG1: PROC( TEXT ) OPTIONS( NODESCRIPTOR BYVALUE ); /* Pass BYVALUE */
    
    DCL TEXT LIST BYADDR NONASGN VARZ CHAR(*);

    DCL IZ      FIXED BIN;
    DCL LTEXT   FIXED BIN;
    DCL PTEXT   POINTER;
    DCL P       POINTER;
    DCL I       FIXED BIN(31) BASED;
    DCL D       FLOAT BIN(53) BASED;
    DCL CHARS   CHAR(32767) BASED;
    DCL CH CHAR(1) BASED;
    
    PTEXT = ADDR(TEXT);
    LTEXT = LENGTH(TEXT);
    
    IZ = INDEX( SUBSTR(PTEXT->CHARS, 1, LTEXT), '%' );
    
    P = VARGLIST();
    
    DO WHILE( IZ > 0 );
    IF IZ = 1 THEN;
    ELSE
        PUT LIST( SUBSTR(PTEXT->CHARS, 1, IZ-1) );
        
        PTEXT += IZ;
        LTEXT -= IZ;
        
        SELECT( PTEXT->CH );
           WHEN( 'I' )
               DO;
                 PUT EDIT( TRIM(P->I) )(A);
                 P += VARGSIZE( P->I );
               END;
              
           WHEN( 'D' )
               DO;
                 PUT EDIT( TRIM(P->D) )(A);
                 P += VARGSIZE( P->D );
               END;
        END;
      
        PTEXT += 1;
        LTEXT -= 1;
                
        IF LTEXT <= 0 THEN LEAVE;
            IZ = INDEX( SUBSTR(PTEXT->CHARS, 1, LTEXT), '%' );
    END;
    
    IF LTEXT = 0 THEN;
    ELSE
       PUT LIST( SUBSTR(PTEXT->CHARS,1, LTEXT) );
    PUT SKIP;
  END;